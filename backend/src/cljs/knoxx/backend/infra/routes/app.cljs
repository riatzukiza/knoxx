(ns knoxx.backend.infra.routes.app
  (:require-macros [knoxx.backend.macros :refer [defroute]])
  (:require [clojure.string :as str]
            [knoxx.backend.extern.fastify :as fastify]
            [knoxx.backend.infra.routes.admin :as admin-routes]
            [knoxx.backend.infra.routes.actors :as actor-routes]
            [knoxx.backend.infra.agent.hydration :refer [ensure-settings! settings-state*]]
            [knoxx.backend.infra.agent.runtime :refer [resolve-workspace-path]]
            [knoxx.backend.infra.agent.runner :as agent-runner]
            [knoxx.backend.infra.agent.service :refer [active-agent-session queue-agent-control! resume-recovered-session! send-agent-turn!]]
            [knoxx.backend.shape.agent :refer [streaming? current-turn]]
            [knoxx.backend.infra.agent.policy :refer [validate-chat-policy!]]
            [knoxx.backend.infra.agent.turn :refer [ensure-conversation-access! ensure-session-id]]
            [knoxx.backend.shape.app-shapes :refer [normalize-chat-body normalize-control-body route!]]
            [knoxx.backend.infra.auth.authz :refer [policy-db policy-db-enabled? policy-db-promise with-request-context! ensure-permission! ensure-tool! ensure-any-permission! ensure-org-scope! primary-context-role ctx-permitted? system-admin? ctx-role-slugs ctx-user-id ctx-user-email ctx-org-id ctx-actor-binding run-visible?]]
            [knoxx.backend.infra.core-memory :refer [fetch-openplanner-session-rows! session-visible? session-matches-page-actor-filter? filter-authorized-memory-hits! authorized-session-ids!]]
            [knoxx.backend.infra.routes.resources :as resource-routes]
            [knoxx.backend.infra.publication-admission-hook :as publication-admission-hook]
            [knoxx.backend.infra.translation-event-writer :as translation-event-writer]
            [knoxx.backend.infra.stores.translation-evidence-registry :as translation-evidence-registry]
            [knoxx.backend.infra.stores.translation-split-registry :as translation-split-registry]
            [knoxx.backend.infra.routes.document-admission :as document-admission]
            [knoxx.backend.infra.routes.publication-reconcile :as reconcile-routes]
            [knoxx.backend.extern.fastify.publications :as publication-routes]
            [knoxx.backend.extern.fastify.document-admission :as document-admission-routes]
            [knoxx.backend.extern.fastify.translation-config :as translation-config-routes]
            [knoxx.backend.extern.fastify.translation-dispatch :as translation-dispatch-routes]
            [knoxx.backend.extern.fastify.translation-review :as translation-review-routes]
            [knoxx.backend.extern.fastify.cms-publication :as cms-publication-routes]
            [knoxx.backend.extern.fastify.cms-documents :as cms-document-routes]
            [knoxx.backend.domain.contracts.sources :as contract-sources]
            [knoxx.backend.infra.document-state :refer [normalize-relative-path]]
            [knoxx.backend.infra.routes.documents :as document-routes]
            [knoxx.backend.law.guards :as guards]
            [knoxx.backend.infra.clients.proxx :as proxx-client]
            [knoxx.backend.infra.clients.openplanner :as openplanner-client]
            [knoxx.backend.infra.http :refer [forward-knoxx-request! json-response! rewrite-localhost-url with-query-param bearer-headers fetch-json openai-auth-error send-fetch-response! request-query-string request-body http-error error-response!]]
            [knoxx.backend.infra.routes.memory :as memory-routes]
            [knoxx.backend.infra.routes.models :as model-routes]
            [knoxx.backend.infra.openplanner.memory :refer [openplanner-memory-search! openplanner-graph-export!]]
            [knoxx.backend.domain.realtime :refer [broadcast-ws!]]
            [knoxx.backend.domain.action.run-state :as run-state :refer [runs* run-order*]]
            [knoxx.backend.shape.parse :refer [parse-positive-int truthy-param?]]
            [knoxx.backend.domain.time :refer [now-iso]]
            [knoxx.backend.infra.stores.mongo-session-store :as session-store]
            [knoxx.backend.infra.stores.session-titles :refer [start-session-title-backfill! session-title-backfill* session-titles* get-cached-session-title! session-title-seed-text heuristic-session-title stored-session-title-entry cache-session-title-entry! resolve-session-title! cache-session-title! normalize-session-title]]
            [knoxx.backend.domain.text :refer [count-occurrences replace-first clip-text]]
            [knoxx.backend.infra.routes.tools :as tool-routes]
            [knoxx.backend.infra.tooling :refer [tool-catalog ensure-role-can-use! email-enabled? effective-agent-contract agent-contract-catalog actor-catalog default-agent-contract-id default-actor-id]]
            [knoxx.backend.domain.voice.turn-control :as turn-control]
            [knoxx.backend.infra.routes.voice :as voice-routes]
            [knoxx.backend.infra.routes.workspace-media :as workspace-media-routes]
            [knoxx.backend.infra.routes.studio :as studio-routes]
            [knoxx.backend.infra.routes.translation :as translation-routes]
            [knoxx.backend.extern.promise :as promise]
            ["node:crypto" :as crypto]
            ["node:fs/promises" :as fs]
            ["node:path" :as path]
            ))


(defn ^:async send-agent-turn-best-effort!
  [runtime config body log-label]
  (try
    (await (send-agent-turn! runtime config body))
    (catch :default err
      (.error js/console log-label err)
      (agent-runner/log-and-record-async-spawn-error! body err))))

(defn ^:async queue-chat-start!
  [runtime config reply agent-ctx policy-model body accepted-response]
  (try
    (await (validate-chat-policy! agent-ctx policy-model))
    (send-agent-turn-best-effort! runtime config body "Async agent chat failed")
    (json-response! reply 202 accepted-response)
    (catch :default err
      (error-response! reply err 429))))

(defn ^:async queue-direct-start!
  [runtime config reply agent-ctx policy-model body accepted-response log-label]
  (try
    (await (validate-chat-policy! agent-ctx policy-model))
    (send-agent-turn-best-effort! runtime config body log-label)
    (json-response! reply 202 accepted-response)
    (catch :default err
      (error-response! reply err 429))))


(defn- compact-agent-spec-overrides
  [agent-spec]
  (into {}
        (remove (fn [[_ value]]
                  (or (nil? value)
                      (and (string? value) (str/blank? value))
                      (and (sequential? value) (empty? value)))))
        agent-spec))

(defn- merged-agent-spec
  [config parsed]
  (let [requested (compact-agent-spec-overrides (or (:agent-spec parsed) {}))
        requested-actor-id (or (get requested :actor-id)
                               (default-actor-id config))
        requested-contract-id (or (get requested :contract-id)
                                  (default-agent-contract-id config requested-actor-id))
        resolved (effective-agent-contract config requested-contract-id requested-actor-id)
        resolved-id (:id resolved)
        merged (merge (select-keys resolved [:role :model :system-prompt :task-prompt :thinking-level :tool-policies :contract-actor-ids :memory-hydration :context-policy :sources])
                      requested)
        runtime-sources (contract-sources/compose-source-refs config
                                                              (:sources resolved)
                                                              (:sources requested))]
    (cond-> (assoc merged :sources runtime-sources)
      requested-actor-id (assoc :actor-id requested-actor-id)
      (seq (:contract-actor-ids resolved)) (assoc :contract-actors (:contract-actor-ids resolved))
      resolved-id (assoc :contract-id resolved-id))))

(defn- requested-role
  [parsed]
  (or (get-in parsed [:agent-spec :role])
      (some-> (:auth-context parsed) :role str str/trim not-empty)
      (some->> (get-in parsed [:auth-context :roleSlugs]) seq first str str/trim not-empty)))

(defn- allow-policy?
  [policy]
  (= "allow" (some-> (:effect policy) str str/lower-case)))

(defn- tool-policy-id
  [policy]
  (some-> (or (:toolId policy) (:tool-id policy)) str str/trim not-empty))

(defn- ctx-tool-policies
  [ctx]
  (vec (or (:toolPolicies ctx)
           (:tool-policies ctx)
           [])))

(defn- parsed-auth-tool-policies
  [parsed]
  (vec (or (get-in parsed [:auth-context :toolPolicies])
           (get-in parsed [:auth-context :tool-policies])
           [])))

(defn- requested-tool-policies
  [parsed]
  (let [from-spec (vec (or (get-in parsed [:agent-spec :tool-policies]) []))
        from-auth (parsed-auth-tool-policies parsed)]
    (cond
      (seq from-spec) from-spec
      (seq from-auth) from-auth
      :else [])))

(defn- effective-tool-policies
  [ctx parsed]
  (let [requested (requested-tool-policies parsed)
        context-policies (ctx-tool-policies ctx)]
    (cond
      (and (nil? ctx) (seq requested)) requested
      (and (nil? ctx) (:auth-context parsed)) (parsed-auth-tool-policies parsed)
      (empty? requested) context-policies
      (system-admin? ctx) requested
      :else (let [allowed (->> context-policies
                               (filter allow-policy?)
                               (keep tool-policy-id)
                               set)]
              (->> requested
                   (filter #(contains? allowed (tool-policy-id %)))
                   vec)))))

(defn- effective-auth-context
  [ctx parsed]
  (let [base ctx
        requested-actor-id (some-> (get-in parsed [:agent-spec :actor-id]) str str/trim not-empty)
        _ (when (and requested-actor-id
                     (not= requested-actor-id (ctx-actor-binding ctx)))
            (throw (ex-info "Actor-scoped requests require their authenticated server binding"
                            {:status 403 :reason :invalid-actor-context})))
        requested-role-slug (requested-role parsed)
        role-slugs (cond
                     (and (nil? base) requested-role-slug) [requested-role-slug]
                     (and requested-role-slug (or (system-admin? ctx)
                                                  (contains? (into #{} (or (:roleSlugs base) [])) requested-role-slug)))
                     [requested-role-slug]
                     :else (vec (or (:roleSlugs base) [])))
        tool-policies (effective-tool-policies ctx parsed)
        resource-policies (or (get-in parsed [:agent-spec :resource-policies])
                              (get-in parsed [:auth-context :resourcePolicies])
                              (:resourcePolicies base))]
    (when (or base requested-actor-id requested-role-slug (seq tool-policies) resource-policies)
      (cond-> (or base {})
        requested-actor-id (assoc :actorId requested-actor-id)
        (seq role-slugs) (assoc :roleSlugs role-slugs)
        (or (seq tool-policies) (some? base)) (assoc :toolPolicies tool-policies)
        resource-policies (assoc :resourcePolicies resource-policies)))))

(defn- auth-context-with-actor
  [ctx actor-id]
  (if-let [actor-id* (some-> actor-id str str/trim not-empty)]
    (assoc (or ctx {}) :actorId actor-id*)
    ctx))

(defn- active-run-summary
  [run session]
  (let [messages   (vec (or (:request_messages run) []))
        user-msg   (some #(when (= "user" (some-> (:role %) str/lower-case)) %) (reverse messages))
        conversation-id (:conversation_id run)]
    {:run_id (:run_id run)
     :session_id (:session_id run)
     :conversation_id (:conversation_id run)
     :status (:status run)
     :model (:model run)
     :created_at (:created_at run)
     :updated_at (:updated_at run)
     :ttft_ms (:ttft_ms run)
     :total_time_ms (:total_time_ms run)
     :input_tokens (:input_tokens run)
     :output_tokens (:output_tokens run)
     :tokens_per_s (:tokens_per_s run)
     :error (:error run)
     :event_count (count (or (:events run) []))
     :tool_receipt_count (count (or (:tool_receipts run) []))
     :has_active_stream (boolean (:has_active_stream session))
     :active_turn_registered (boolean (and conversation-id (turn-control/active-turn conversation-id)))
     :agent_spec (get-in run [:settings :agentSpec])
     :resource_policies (get-in run [:resources :agentResourcePolicies])
     :latest_user_message (:content user-msg)
     :content_parts (mapv (fn [p]
                            (-> p
                                (dissoc :data)
                                (select-keys [:type :url :mimeType :filename :text])))
                          (or (:content-parts user-msg) []))
     :tool_receipts (mapv (fn [r]
                            (select-keys r [:id :tool_name :status
                                            :input_preview :result_preview
                                            :started_at :ended_at]))
                          (or (:tool_receipts run) []))
     :trace_blocks (mapv (fn [b]
                           (select-keys b [:id :kind :status :toolName
                                           :toolCallId :content :at
                                           :inputPreview :outputPreview :isError]))
                         (or (:trace_blocks run) []))
     :latest_event (some-> (:events run) last (select-keys [:type :status :tool_name :preview :at]))}))

(defn- active-session-summary
  [session]
  (let [messages (vec (or (:messages session) []))
        user-msg (some #(when (= "user" (some-> (:role %) str/lower-case)) %) (reverse messages))
        conversation-id (:conversation_id session)]
    {:run_id (:run_id session)
     :session_id (:session_id session)
     :conversation_id conversation-id
     :status (:status session)
     :model (:model session)
     :created_at (:created_at session)
     :updated_at (:updated_at session)
     :error (:error session)
     :event_count 0
     :tool_receipt_count 0
     :has_active_stream (boolean (:has_active_stream session))
     :active_turn_registered (boolean (and conversation-id (turn-control/active-turn conversation-id)))
     :agent_spec (or (:agent_spec session) (:agentSpec session))
     :resource_policies (:resource_policies session)
     :latest_user_message (:content user-msg)
     :latest_event nil}))

(defn- live-session-items
  [run-items include-all? sessions]
  (let [run-session-ids (set (keep :session_id run-items))]
    (->> sessions
         (filter some?)
         (filter #(or include-all?
                      (contains? #{"queued" "running" "waiting_input"} (:status %))))
         (remove #(contains? run-session-ids (:session_id %)))
         (map active-session-summary)
         vec)))

(defn- active-item-time-ms
  [item]
  (let [value (or (:updated_at item) (:created_at item))]
    (cond
      (number? value) value
      (string? value) (let [parsed (js/Date.parse value)]
                        (if (js/isNaN parsed) 0 parsed))
      :else 0)))

(defn- sort-active-items
  [limit items]
  (->> items
       (sort-by active-item-time-ms #(compare %2 %1))
       (take limit)
       vec))

(defn ^:async live-active-agent-summaries!
  [limit include-all?]
  (let [limit (max 1 (or limit 25))
         sessions-by-id (into {}
                             (map (fn [session]
                                    [(:session_id session) session]))
                             (session-store/active-session-snapshots))
        run-items (->> @run-order*
                       (map #(get @runs* %))
                       (filter some?)
                       (filter #(contains? #{"queued" "running" "waiting_input"} (:status %)))
                       (map (fn [run]
                              (active-run-summary run (get sessions-by-id (:session_id run)))))
                                               vec)]
    (sort-active-items limit (concat run-items (live-session-items run-items include-all? (await (session-store/list-active-sessions)))))))

(def SESSION_RECOVERY_STALE_MS 60000)

(defn- clear-ghost-turn!
  "If turn-control has an entry for conversation-id but the underlying Proxx
   session shows no active streaming or current turn, the entry is a ghost
   from a previous hung run. Unregister it so zombie recovery can proceed."
  [conversation-id]
  (let [agent-session (active-agent-session conversation-id)
        active-streaming? (and agent-session (streaming? agent-session))
        active-turn? (and agent-session
                          (try
                            (some? (current-turn agent-session))
                            (catch js/Error _ false)))]
    (when (and (not active-streaming?) (not active-turn?))
      (turn-control/unregister-active-turn! conversation-id))))

(defn- runtime-processing-session?
  [conversation-id]
  (let [agent-session (active-agent-session conversation-id)
        active-streaming? (and agent-session (streaming? agent-session))
        active-turn? (and agent-session
                          (try
                            (some? (current-turn agent-session))
                            (catch js/Error _ false)))
        registered-turn? (some? (turn-control/active-turn conversation-id))]
    (or active-streaming? active-turn? registered-turn?)))

(defn- parse-iso-ms
  [value]
  (let [parsed (js/Date.parse (str (or value "")))]
    (when-not (js/isNaN parsed)
      parsed)))

(defn ^:async latest-run-event!
  [run-id]
  (let [run-id (str (or run-id ""))]
    (cond
      (str/blank? run-id)
      nil

      (seq (get-in @runs* [run-id :events]))
      (last (get-in @runs* [run-id :events]))

      :else
      nil)))

(defn- stale-running-session?
  [session latest-event]
  (let [stamp (or (:at latest-event)
                  (:updated_at session)
                  (:created_at session))
        stamp-ms (parse-iso-ms stamp)]
    (or (nil? stamp-ms)
        (> (- (.now js/Date) stamp-ms) SESSION_RECOVERY_STALE_MS))))

(defn- dev-hmr-response
  []
  {:ok true
   :version "v2"
   :at (now-iso)})

;; ── Common promise handlers ───────────────────────────────────────────

(defn- proxy-err [reply prefix err]
  (json-response! reply 502 {:detail (str prefix err)}))

(defn- fetch-json-ok [reply resp]
  (json-response! reply (or (:status resp) 200) (:body resp)))

(defn- fetch-json-err [reply err]
  (json-response! reply 502 {:error (.-message err)}))

(defn- fetch-json-err-detail [reply prefix err]
  (json-response! reply 502 {:detail (str prefix err)}))

(defn- dependency-status
  [configured reachable result]
  {:configured configured
   :reachable (boolean reachable)
   :status_code (:status result)
   :detail (:body result)})

(defn- health-deps-ok [reply proxx-configured openplanner-configured ollama-configured
                       [proxx-res openplanner-res ollama-res]]
  (let [proxx-ok       (and proxx-configured (:ok proxx-res))
        openplanner-ok (and openplanner-configured (:ok openplanner-res))
        ollama-ok      (and ollama-configured (:ok ollama-res))
        healthy        (and proxx-ok
                            openplanner-ok
                            (or (not ollama-configured) ollama-ok))]
    (json-response!
     reply
     (if healthy 200 503)
     {:status (if healthy "ok" "unhealthy")
      :service "knoxx-backend-cljs"
      :dependencies {:proxx (dependency-status proxx-configured proxx-ok proxx-res)
                     :openplanner (dependency-status openplanner-configured openplanner-ok openplanner-res)
                     :ollama (dependency-status ollama-configured ollama-ok ollama-res)}})))

(defn- health-deps-err [reply err]
  (json-response! reply 503 {:status "unhealthy"
                             :service "knoxx-backend-cljs"
                             :error (str err)}))

(defn- knoxx-health-ok [reply config proxx-configured openplanner-configured ollama-configured
                        [proxx-res openplanner-res ollama-res]]
  (let [proxx-ok       (and proxx-configured (:ok proxx-res))
        openplanner-ok (and openplanner-configured (:ok openplanner-res))
        ollama-ok      (and ollama-configured (:ok ollama-res))
        ;; Healthy only when every configured dependency is reachable.
        healthy        (and (or (not proxx-configured) proxx-ok)
                            (or (not openplanner-configured) openplanner-ok)
                            (or (not ollama-configured) ollama-ok)
                            (or proxx-configured openplanner-configured ollama-configured))]
    (json-response!
     reply
     (if healthy 200 503)
     {:reachable healthy
      :configured (boolean (or proxx-configured openplanner-configured ollama-configured))
      :base_url (:knoxx-base-url config)
      :status_code (if healthy 200 503)
      :status (if healthy "ok" "unhealthy")
      :details {:mode "shadow-cljs-eta-mu-sdk"
                :status (if healthy "ok" "unhealthy")
                :project (:project-name config)
                :collection {:name (:collection-name config)
                             :pointsCount nil}
                :dependencies
                {:proxx (dependency-status proxx-configured proxx-ok proxx-res)
                 :openplanner (dependency-status openplanner-configured openplanner-ok openplanner-res)
                 :ollama (dependency-status ollama-configured ollama-ok ollama-res)}}})))

(defn- knoxx-health-err [reply config err]
  (json-response! reply 503 {:reachable false
                             :configured true
                             :base_url (:knoxx-base-url config)
                             :status_code 503
                             :status "unhealthy"
                             :error (str err)}))

(defn- data-health-ok [reply results]
  (json-response! reply 200
                  {:ok true
                   :services {:openplanner (nth results 0)
                              :proxx (nth results 1)
                              :ingestion (nth results 2)
                              :graph-weaver (nth results 3)
                              :shuvcrawl (nth results 4)
                              :vexx (nth results 5)
                              :eros-eris-field-app (nth results 6)
                              :myrmex (nth results 7)
                              :ollama (nth results 8)}}))

(defn- data-health-err [reply err]
  (json-response! reply 500 {:error (.-message err)}))

(declare mongo-collections-ok)

(defn ^:async send-knoxx-proxy!
  [config request reply method path]
  (try
    (send-fetch-response! reply (await (forward-knoxx-request! config request method path nil)))
    (catch :default err
      (proxy-err reply "Proxy request failed: " err))))

(defn ^:async send-fetch-json!
  [reply target-url opts error-handler]
  (try
    (fetch-json-ok reply (await (fetch-json target-url opts)))
    (catch :default err
      (error-handler reply err))))

(defn ^:async send-fetch-json-detail!
  [reply target-url opts prefix]
  (send-fetch-json! reply target-url opts #(fetch-json-err-detail %1 prefix %2)))

(defn ^:async send-openplanner-v1-json!
  [config reply method path body]
  (try
    (json-response! reply 200 (await (openplanner-client/v1-json!
                                      (openplanner-client/client config) method path body)))
    (catch :default err
      (fetch-json-err reply err))))

(defn ^:async send-json-promise!
  [reply request-promise]
  (try
    (json-response! reply 200 (await request-promise))
    (catch :default err
      (fetch-json-err reply err))))

(defn ^:async send-mongo-collections!
  [config reply]
  (let [client (openplanner-client/client config)]
    (try
      (let [[docs graph] (await (promise/all-vec
                                 [(openplanner-client/documents-stats! client)
                                  (openplanner-client/graph-monitoring! client)]))]
        (mongo-collections-ok reply [{:ok true :body docs}
                                     {:ok true :body graph}]))
      (catch :default err
        (fetch-json-err reply err)))))

(defn ^:async send-data-browse!
  [reply target-url]
  (try
    (let [resp (await (fetch-json target-url nil))]
      (json-response! reply
                      (or (:status resp) (aget resp "status") 200)
                      (or (:body resp) (aget resp "body"))))
    (catch :default err
      (fetch-json-err reply err))))

(defn ^:async write-ingestion-file!
  [reply absolute-path content safe-path]
  (try
    (await (.mkdir fs (.dirname path absolute-path) (clj->js {:recursive true})))
    (await (.writeFile fs absolute-path content "utf8"))
    (json-response! reply 200 {:ok true :path safe-path})
    (catch :default err
      (json-response! reply 500 {:detail (str "Write failed: " err)}))))

(defn- health-result
  [url resp]
  {:ok (:ok resp) :status (:status resp) :url url :detail (:body resp)})

(defn- health-error-result
  [url err]
  {:ok false :error (.-message err) :url url})

(defn ^:async service-health-check!
  [url headers]
  (try
    (health-result url (await (fetch-json url {:headers (or headers {}) :method "GET"})))
    (catch :default err
      (health-error-result url err))))

(defn ^:async openplanner-health-check!
  [config]
  (try
    (health-result "openplanner:/v1/health"
                   (await (openplanner-client/health! (openplanner-client/client config))))
    (catch :default err
      (health-error-result "openplanner:/v1/health" err))))

(defn ^:async proxx-health-check!
  [config]
  (let [url (str (:proxx-base-url config) "/health")]
    (try
      (health-result url (await (proxx-client/health! (proxx-client/client config))))
      (catch :default err
        (health-error-result url err)))))

(defn ^:async ollama-health-check!
  [config]
  (if (str/blank? (:ollama-base-url config))
    {:ok false :configured false :url nil :detail {:status "not configured"}}
    (service-health-check! (str (str/replace (:ollama-base-url config) #"/+$" "")
                                "/api/version")
                           nil)))

(defn- unavailable-health
  [service-name]
  (js/Promise.resolve {:ok false
                       :status 503
                       :body {:detail (str service-name " is not configured")}}))

(defn- dependency-probes
  [config]
  (let [proxx-configured (and (not (str/blank? (:proxx-base-url config)))
                              (not (str/blank? (:proxx-auth-token config))))
        ollama-configured (not (str/blank? (:ollama-base-url config)))
        openplanner (openplanner-client/client config)
        openplanner-configured (openplanner-client/enabled? openplanner)]
    {:proxx-configured proxx-configured
     :openplanner-configured openplanner-configured
     :ollama-configured ollama-configured
     :promises [(if proxx-configured
                  (proxx-client/health! (proxx-client/client config))
                  (unavailable-health "Proxx"))
                (if openplanner-configured
                  (openplanner-client/health! openplanner)
                  (unavailable-health "OpenPlanner"))
                (if ollama-configured
                  (ollama-health-check! config)
                  (unavailable-health "Ollama"))]}))

(defn ^:async send-data-health!
  [config reply]
  (let [ingestion-base (:ingestion-base-url config)]
    (try
      (data-health-ok reply
                      (await (promise/all-vec
                              [(openplanner-health-check! config)
                               (proxx-health-check! config)
                               (service-health-check! (str ingestion-base "/health") nil)
                               (service-health-check! "http://127.0.0.1:8796/api/status" nil)
                               (service-health-check! "http://127.0.0.1:3777/health" nil)
                               (service-health-check! "http://127.0.0.1:8787/v1/health" nil)
                               (service-health-check! "http://127.0.0.1:8786/health" nil)
                               (service-health-check! "http://127.0.0.1:8801/health" nil)
                               (ollama-health-check! config)])))
      (catch :default err
        (data-health-err reply err)))))

(defn ^:async send-knoxx-health!
  [config reply]
  (let [{:keys [proxx-configured openplanner-configured ollama-configured promises]}
        (dependency-probes config)]
    (try
      (knoxx-health-ok reply
                       config
                       proxx-configured
                       openplanner-configured
                       ollama-configured
                       (await (promise/all-vec promises)))
      (catch :default err
        (knoxx-health-err reply config err)))))

(def deps
  {:route! route!
   :json-response! json-response!
   :error-response! error-response!
   :ensure-permission! ensure-permission!
   :clip-text clip-text
   :with-request-context! with-request-context!
   :send-fetch-response! send-fetch-response!
   :bearer-headers bearer-headers
   :fetch-json fetch-json
   :request-query-string request-query-string
   :session-guard nil
   :optional-session-guard nil})

(defn- response-body
  [resp]
  (or (:body resp) (aget resp "body")))

(defn- mongo-collections-ok [reply results]
  (json-response! reply 200
                  {:ok true
                   :documents (response-body (nth results 0))
                   :graph (response-body (nth results 1))}))

(defn- undo-session-err [reply err]
  (json-response! reply 500 {:ok false :error (str err)}))

(defn- agents-active-ok [reply items]
  (json-response! reply 200 {:runs items :count (count items)}))

(defn- agents-active-err [reply err]
  (error-response! reply err 502))

(defn- run-events-ok [reply run-id events]
  (json-response! reply 200 {:run_id run-id :events events :count (count events)}))

(defn- run-events-err [reply err]
  (json-response! reply 500 {:error (str err)}))

(defn- shibboleth-ok [config reply request body data]
  (let [session (or (:session data) {})
        session-id (str (or (:id session) ""))
        ui-url (if (and (not (str/blank? session-id))
                        (not (str/blank? (:shibboleth-ui-url config))))
                 (with-query-param (rewrite-localhost-url (:shibboleth-ui-url config) request)
                   "session"
                   session-id)
                 "")]
    (if (str/blank? session-id)
      (json-response! reply 502 {:detail "Shibboleth import did not return a session id"})
      (json-response! reply 200 {:ok true
                                 :session_id session-id
                                 :ui_url ui-url
                                 :imported_item_count (count (or (:items body) []))}))))

(defn- shibboleth-import-failed [reply resp]
  (json-response! reply 502 {:detail (str "Shibboleth import failed: "
                                          (or (:raw (:body resp))
                                              (js/JSON.stringify (clj->js (:body resp)))))}))

(defn- shibboleth-unreachable [reply err]
  (json-response! reply 502 {:detail (str "Shibboleth is unreachable: " err)}))

(defn ^:async detect-zombies
  [conversation-id session session-id queue-turn! can-send-result reply latest-event]
  (clear-ghost-turn! conversation-id)
  (let [stalled? (and (= "running" (:status session))
                      (not (runtime-processing-session? conversation-id))
                      (stale-running-session? session latest-event))]
    (if stalled?
      (try
        (await (session-store/complete-session! session-id
                                                conversation-id
                                                {:status "failed"
                                                 :error "Session was stale/zombie; auto-aborted before new turn."
                                                 :messages (:messages session)}))
        (await (queue-turn! "Async direct agent chat failed (recovered from zombie)"))
        (catch :default err
          (.error js/console "Failed to abort zombie session" err)
          (json-response! reply 409 {:ok false
                                     :error (str "Agent is already processing. Zombie recovery failed: " err)
                                     :code "agent_already_processing"
                                     :has_active_stream false
                                     :can_send false})))
      (json-response! reply 409 {:ok false
                                 :error (str "Agent is already processing. " (or (:reason can-send-result) ""))
                                 :code "agent_already_processing"
                                 :has_active_stream (boolean (:has_active_stream session))
                                 :can_send false}))))

(defn ^:async handle-chat-start [runtime config reply ctx request]
  (let [node-crypto crypto
        parsed0 (normalize-chat-body (request-body request))
        parsed (assoc parsed0 :agent-spec (merged-agent-spec config parsed0))
        agent-ctx (effective-auth-context ctx parsed)
        policy-model (or (:model parsed)
                         (get-in parsed [:agent-spec :model])
                         (:llmModel @settings-state*))
        provided-session-id (:session-id parsed)
        session-id (ensure-session-id provided-session-id)
        conversation-id (or (:conversation-id parsed)
                            (.randomUUID node-crypto))
        run-id (or (:run-id parsed)
                   (.randomUUID node-crypto))
        body (assoc parsed
                    :session-id session-id
                    :conversation-id conversation-id
                    :run-id run-id
                    :mode "rag"
                    :auth-context agent-ctx)
        accepted-response {:ok true
                           :queued true
                           :run_id run-id
                           :runId run-id
                           :conversation_id conversation-id
                           :conversationId conversation-id
                           :session_id session-id
                           :sessionId session-id
                           :model (or (:model body)
                                      (get-in body [:agent-spec :model])
                                      (:llmModel @settings-state*))}
        queue-turn! (fn [_log-label]
                      (queue-chat-start! runtime config reply agent-ctx policy-model body accepted-response))]
    (if-not provided-session-id
      (await (queue-turn! "Async agent chat failed"))
      (try
         (let [session (await (session-store/get-session session-id))
               can-send-result (session-store/session-can-send? session)]
           (if (:can-send can-send-result)
             (let [agent-session (active-agent-session conversation-id)
                   actively-streaming? (and agent-session (streaming? agent-session))]
               (if actively-streaming?
                 (json-response! reply 409
                                 {:ok false
                                  :error "Agent is already processing. Specify streamingBehavior steer or followUp to queue the message."
                                  :code "agent-already-processing"
                                 :has-active-stream true
                                 :can-send false})
                 (await (queue-turn! "Async agent chat failed"))))
            (let [latest-event (when (= "running" (:status session))
                                 (await (latest-run-event! (:run_id session))))]
              (await (detect-zombies conversation-id session session-id queue-turn!
                                     can-send-result reply latest-event)))))
        (catch :default err
          (.error js/console "Session status check failed" err)
          (await (queue-turn! "Async agent chat failed")))))))

(defn ^:async handle-direct-start [runtime config reply ctx request]
  (let [node-crypto crypto
        parsed0 (normalize-chat-body (request-body request))
        parsed (assoc parsed0 :agent-spec (merged-agent-spec config parsed0))
        agent-ctx (effective-auth-context ctx parsed)
        policy-model (or (:model parsed)
                         (get-in parsed [:agent-spec :model])
                         (:llmModel @settings-state*))
        provided-session-id (:session-id parsed)
        session-id (ensure-session-id provided-session-id)
        conversation-id (or (:conversation-id parsed) (.randomUUID node-crypto))
        run-id (or (:run-id parsed) (.randomUUID node-crypto))
        body (assoc parsed :session-id session-id :conversation-id conversation-id :run-id run-id :mode "direct" :auth-context agent-ctx)
        accepted-response {:ok true
                           :queued true
                           :run_id run-id
                           :conversation_id conversation-id
                           :session_id (:session-id body)
                           :model (or (:model body)
                                      (get-in body [:agent-spec :model])
                                      (:llmModel @settings-state*))}
        queue-turn! (fn [log-label]
                      (queue-direct-start! runtime config reply agent-ctx policy-model
                                           body accepted-response log-label))]
    (if-not provided-session-id
      (await (queue-turn! "Async direct agent chat failed"))
      (try
        (let [session (await (session-store/get-session session-id))
              can-send-result (session-store/session-can-send? session)]
          (if (:can-send can-send-result)
            (let [agent-session (active-agent-session conversation-id)
                  actively-streaming? (and agent-session (streaming? agent-session))]
              (if actively-streaming?
                (json-response! reply 409 {:ok false
                                           :error "Agent is already processing. Specify streamingBehavior ('steer' or 'followUp') to queue the message."
                                           :code "agent_already_processing"
                                           :has_active_stream true
                                           :can_send false})
                (await (queue-turn! "Async direct agent chat failed"))))
            (let [latest-event (when (= "running" (:status session))
                                 (await (latest-run-event! (:run_id session))))]
              (await (detect-zombies conversation-id session session-id queue-turn!
                                     can-send-result reply latest-event)))))
        (catch :default err
          (.error js/console "Session status check failed" err)
          (await (queue-turn! "Async direct agent chat failed")))))))

(defn- ^:async handle-admin-abort [reply _ctx request]
  (let [raw (request-body request)
        requested-conversation-id (str (or (aget raw "conversation_id")
                                           (aget raw "conversationId")
                                           ""))
        requested-session-id (str (or (aget raw "session_id")
                                      (aget raw "sessionId")
                                      ""))
        requested-run-id (str (or (aget raw "run_id")
                                  (aget raw "runId")
                                  ""))
         reason (str (or (aget raw "reason") "operator_abort"))
         run (when-not (str/blank? requested-run-id)
               (get @runs* requested-run-id))
         session-id (or (some-> requested-session-id not-empty)
                        (:session_id run))]
     (try
       (let [session (await (if (not (str/blank? (str session-id)))
                              (session-store/get-session session-id)
                              (js/Promise.resolve nil)))
             conversation-id (str (or (some-> requested-conversation-id not-empty)
                                      (:conversation_id run)
                                      (:conversation_id session)
                                      ""))
             resolved-session-id (str (or session-id (:session_id session) ""))
             resolved-run-id (str (or requested-run-id (:run_id run) (:run_id session) ""))]
         (if (str/blank? conversation-id)
           (json-response! reply 400 {:ok false
                                      :error "conversation_id, session_id, or run_id is required"})
           (let [abort-result (await (turn-control/abort-active-turn! conversation-id reason))]
             (when-not (str/blank? resolved-run-id)
               (run-state/update-run! resolved-run-id
                                      (fn [r]
                                        (-> r
                                            (assoc :status "aborted"
                                                   :error reason
                                                   :updated_at (now-iso))))))
             (await (if (not (str/blank? resolved-session-id))
                      (session-store/update-session! resolved-session-id
                                                     {:status "aborted"
                                                      :error reason
                                                      :has_active_stream false})
                      (js/Promise.resolve nil)))
             (json-response! reply 200
                             (assoc abort-result
                                    :ok true
                                    :conversation_id conversation-id
                                    :session_id resolved-session-id
                                    :run_id resolved-run-id
                                    :marked_aborted true)))))
       (catch :default err
         (error-response! reply err 409)))))

(defn- session-status-running-response
  [session-id session runtime-active? can-send stalled? latest-event]
  {:session_id session-id
   :conversation_id (:conversation_id session)
   :run_id (:run_id session)
   :status (:status session)
   :has_active_stream (boolean (or (:has_active_stream session) runtime-active?))
   :can_send (if stalled? false (:can-send can-send))
   :reason (cond
             stalled? "Session looked stalled after restart; recovery requested."
             runtime-active? "Session is already processing. Use steer, follow-up, abort, or wait."
             :else (:reason can-send))
   :model (:model session)
   :updated_at (:updated_at session)
   :latest_event_at (:at latest-event)
   :recovery_requested stalled?})

(defn- ^:async handle-session-status [runtime config reply request]
  (let [session-id (or (aget request "query" "session_id")
                       (aget request "query" "sessionId")
                       "")
        conversation-id (or (aget request "query" "conversation_id")
                            (aget request "query" "conversationId")
                            "")]
    (cond
      (str/blank? session-id)
      (json-response! reply 400 {:error "session_id is required"})

      :else
      (try
        (let [session (await (session-store/get-session session-id))]
          (if session
            (let [conversation-id' (str (or (:conversation_id session) conversation-id ""))
                  runtime-active? (runtime-processing-session? conversation-id')
                  can-send (session-store/session-can-send? session)
                  latest-event (await (if (= "running" (:status session))
                                        (latest-run-event! (:run_id session))
                                        (js/Promise.resolve nil)))
                  stalled? (and (= "running" (:status session))
                                (not runtime-active?)
                                (stale-running-session? session latest-event))]
              (when stalled?
                ((^:async fn []
                   (try
                     (await (resume-recovered-session! runtime config session))
                     (catch :default err
                       (js/console.error "On-demand session recovery failed" err))))))
              (json-response! reply 200
                              (session-status-running-response
                               session-id session runtime-active? can-send stalled? latest-event)))
            ;; No session in the store - trust in-memory runtime if it still has a live turn.
            (if (runtime-processing-session? conversation-id)
              (json-response! reply 200
                              {:session_id session-id
                               :conversation_id conversation-id
                               :status "running"
                               :has_active_stream true
                               :can_send false
                               :reason "Session is already processing. Use steer, follow-up, abort, or wait."})
              (json-response! reply 200
                              {:session_id session-id
                               :conversation_id conversation-id
                               :status "not_found"
                               :has_active_stream false
                               :can_send true
                               :reason "No session state found. Ready for new turn."}))))
        (catch :default err
          (js/console.error "Session status check failed" err)
          (json-response! reply 500 {:error (str err)}))))))

(defroute health! []
  "GET" "/health"
  (let [{:keys [proxx-configured openplanner-configured ollama-configured promises]}
        (dependency-probes config)]
    (try
      (health-deps-ok reply proxx-configured openplanner-configured ollama-configured
                      (await (promise/all-vec promises)))
      (catch :default err
        (health-deps-err reply err)))))

;; Dev-only endpoint used to verify shadow-cljs hot-reload against a live
;; long-running Node runtime (shadow owns the process).
(defroute dev-hmr! []
  "GET" "/api/dev/hmr" []
  (json-response! reply 200 (dev-hmr-response)))

(defroute config! []
  "GET" "/api/config"
  (json-response!
   reply
   200
   {:knoxx_admin_url (rewrite-localhost-url (:knoxx-admin-url config) request)
    :knoxx_base_url (rewrite-localhost-url (:knoxx-base-url config) request)
    :knoxx_enabled true
    :stt_enabled (not (str/blank? (:stt-base-url config)))
    :stt_base_url (if (str/blank? (:stt-base-url config))
                    ""
                    (rewrite-localhost-url (:stt-base-url config) request))
    :tts_enabled (not (str/blank? (str/trim (or (:voxx-api-key config) ""))))
    :tts_provider (if (not (str/blank? (str/trim (or (:voxx-api-key config) ""))))
                    "voxx"
                    "")
    :tts_default_voice_id (or (:voxx-voice-id config) "af_jessica")
    :tts_default_model_id (or (:voxx-model-id config) "kokoro")
    :tts_default_speed (or (:voxx-default-speed config) "1.15")
    :tts_default_postprocess_enabled true
    :tts_default_postprocess_profile "sports-commentator-v1"
    :proxx_enabled (and (not (str/blank? (:proxx-base-url config)))
                        (not (str/blank? (:proxx-auth-token config))))
    :ollama_enabled (not (str/blank? (:ollama-base-url config)))
    :ollama_default_model (:ollama-default-model config)
    :proxx_default_model (:llmModel @settings-state*)
    :shibboleth_ui_url (if (str/blank? (:shibboleth-ui-url config))
                         ""
                         (rewrite-localhost-url (:shibboleth-ui-url config) request))
    :shibboleth_enabled (and (not (str/blank? (:shibboleth-base-url config)))
                             (not (str/blank? (:shibboleth-ui-url config))))
    :default_role (:knoxx-default-role config)
    :default_actor_id (default-actor-id config)
    :default_agent_contract (default-agent-contract-id config (default-actor-id config))
    :email_enabled (email-enabled? config)
    :rbac_enabled (policy-db-enabled? runtime)}))

(defroute api-knoxx-agents-catalog! []
  "GET" "/api/knoxx/agents/catalog"
  (when ctx (ensure-permission! ctx "agent.chat.use"))
  (let [actor-id (some-> (or (aget request "query" "actorId")
                             (aget request "query" "actor"))
                         str
                         str/trim
                         not-empty)
        effective-actor-id (or actor-id (default-actor-id config))
        agents (agent-contract-catalog config effective-actor-id)
        default-agent-id (default-agent-contract-id config effective-actor-id)
        default-agent (when default-agent-id
                        (effective-agent-contract config default-agent-id effective-actor-id))
        catalog (cond-> agents
                  (and default-agent
                       (not (some #(= (:id %) (:id default-agent)) agents)))
                  (conj default-agent))]
    (json-response! reply 200 {:actor_id effective-actor-id
                               :actors (mapv (fn [actor]
                                               {:id (:id actor)
                                                :kind (:kind actor)
                                                :defaultAgent (:default-agent actor)
                                                :roleSlugs (vec (or (:role-slugs actor) []))})
                                             (actor-catalog config))
                               :agents (vec (sort-by :id catalog))
                               :default_actor_id (default-actor-id config)
                               :default_agent_contract default-agent-id})))

(defroute api-auth-context! []
  "GET" "/api/auth/context"
  (if-not (policy-db-enabled? runtime)
    (json-response! reply 503 {:detail "Knoxx policy database is not configured"})
    (json-response! reply 200 {:user (:user ctx)
                               :actor (:actor ctx)
                               :org (:org ctx)
                               :membership (:membership ctx)
                               :roles (vec (or (:roles ctx) []))
                               :roleSlugs (vec (ctx-role-slugs ctx))
                               :permissions (vec (or (:permissions ctx) []))
                               :toolPolicies (vec (or (:tool-policies ctx) (:toolPolicies ctx) []))
                               :membershipToolPolicies (vec (or (:membership-tool-policies ctx) (:membershipToolPolicies ctx) []))
                               :isSystemAdmin (system-admin? ctx)
                               :primaryRole (primary-context-role ctx)})))

(defroute api-knoxx-proxy-get! []
  "GET" "/api/knoxx/proxy/*"
  (send-knoxx-proxy! config request reply "GET" (aget request "params" "*")))

(defroute api-knoxx-proxy-post! []
  "POST" "/api/knoxx/proxy/*"
  (send-knoxx-proxy! config request reply "POST" (aget request "params" "*")))

(defroute api-knoxx-proxy-put! []
  "PUT" "/api/knoxx/proxy/*"
  (send-knoxx-proxy! config request reply "PUT" (aget request "params" "*")))

(defroute api-knoxx-proxy-patch! []
  "PATCH" "/api/knoxx/proxy/*"
  (send-knoxx-proxy! config request reply "PATCH" (aget request "params" "*")))

(defroute api-knoxx-proxy-delete! []
  "DELETE" "/api/knoxx/proxy/*"
  (send-knoxx-proxy! config request reply "DELETE" (aget request "params" "*")))

(defroute api-ingestion-browse! []
  "GET" "/api/ingestion/browse"
  (let [target-url (str (:ingestion-base-url config) "/api/ingestion/browse"
                        (request-query-string request))]
    (send-fetch-json! reply target-url {:method "GET"} fetch-json-err)))

(defroute api-ingestion-file! []
  "GET" "/api/ingestion/file"
  (let [target-url (str (:ingestion-base-url config) "/api/ingestion/file"
                        (request-query-string request))]
    (send-fetch-json! reply target-url {:method "GET"} fetch-json-err)))

(defroute api-ingestion-file-put! []
  "PUT" "/api/ingestion/file"
  (let [workspace-root (:workspace-root config)
        body (request-body request)
        file-path (or (aget body "path") "")
        content (or (aget body "content") "")
        safe-path (normalize-relative-path file-path)]
    (if (or (str/blank? safe-path) (str/starts-with? safe-path ".."))
      (json-response! reply 400 {:detail "Invalid or unsafe file path"})
      (let [absolute-path (.resolve path workspace-root safe-path)]
        (if (not (str/starts-with? absolute-path workspace-root))
          (json-response! reply 400 {:detail "Path escapes workspace"})
          (write-ingestion-file! reply absolute-path content safe-path))))))

(defroute api-ingestion-sources! []
  "GET" "/api/ingestion/sources"
  (send-fetch-json! reply
                    (str (:ingestion-base-url config) "/api/ingestion/sources")
                    {:method "GET"}
                    fetch-json-err))

(defroute api-ingestion-jobs-get! []
  "GET" "/api/ingestion/jobs"
  (let [target-url (str (:ingestion-base-url config) "/api/ingestion/jobs"
                        (request-query-string request))]
    (send-fetch-json! reply target-url {:method "GET"} fetch-json-err)))

(defroute api-ingestion-jobs-post! []
  "POST" "/api/ingestion/jobs"
  (let [body (aget request "body")]
    (send-fetch-json! reply
                      (str (:ingestion-base-url config) "/api/ingestion/jobs")
                      {:method "POST"
                       :headers {"Content-Type" "application/json"}
                       :body (or (some-> body js/JSON.stringify) "{}")}
                      fetch-json-err)))

(defroute api-ingestion-proxy-get! []
  "GET" "/api/ingestion-proxy/*"
  (let [ingestion-base (:ingestion-base-url config)
        path (aget request "params" "*")
        qs (request-query-string request)
        target-url (str ingestion-base "/api/ingestion/" path qs)]
    (send-fetch-json-detail! reply target-url {:method "GET"} "Ingestion proxy failed: ")))

(defroute api-ingestion-proxy-post! []
  "POST" "/api/ingestion-proxy/*"
  (let [ingestion-base (:ingestion-base-url config)
        path (aget request "params" "*")
        target-url (str ingestion-base "/api/ingestion/" path)
        body (aget request "body")]
    (send-fetch-json-detail! reply
                             target-url
                             {:method "POST"
                              :headers {"Content-Type" "application/json"}
                              :body (or (some-> body js/JSON.stringify) "{}")}
                             "Ingestion proxy failed: ")))

(defroute api-ingestion-proxy-delete! []
  "DELETE" "/api/ingestion-proxy/*"
  (let [ingestion-base (:ingestion-base-url config)
        path (aget request "params" "*")
        target-url (str ingestion-base "/api/ingestion/" path)]
    (send-fetch-json-detail! reply target-url {:method "DELETE"} "Ingestion proxy failed: ")))

(defroute api-data-op-get! []
  "GET" "/api/data/op/*"
  (let [path (aget request "params" "*")
        raw-url (aget request "raw" "url")
        query-idx (.indexOf raw-url "?")
        qs (if (>= query-idx 0) (subs raw-url query-idx) "")]
    (send-openplanner-v1-json! config reply "GET" (str path qs) nil)))

(defn- canonical-openplanner-proxy-path
  [path]
  (let [decoded (loop [value (str path)
                       attempts 0]
                  (if (or (not (str/includes? value "%"))
                          (= attempts 16))
                    value
                    (let [next-value (try
                                       (fastify/decode-uri-component value)
                                       (catch :default _ value))]
                      (if (= value next-value)
                        value
                        (recur next-value (inc attempts))))))
        segments (->> (str/split (-> decoded
                                      str/lower-case
                                      (str/replace "\\" "/"))
                                  #"/+")
                      (reduce (fn [result segment]
                                (case segment
                                  "" result
                                  "." result
                                  ".." (if (seq result) (pop result) result)
                                  (conj result segment)))
                              []))]
    (if (= "v1" (first segments))
      (vec (rest segments))
      segments)))

(defn authorize-openplanner-proxy-post!
  "Refuse authenticated vector search until OpenPlanner can enforce org scope.

  The current SDK only accepts a fixed non-tenant `where` allowlist and drops
  `org_id` from stored vector documents. Response-side filtering would disclose
  rows to this process and truncate top-k results, so the service-credential
  compatibility proxy fails closed before forwarding. Policy-disabled local
  mode remains the existing explicitly trusted workstation boundary."
  [ctx path body]
  (when (and ctx
             (= ["search" "vector"]
                (canonical-openplanner-proxy-path path)))
    (throw (http-error 403
                       "openplanner_vector_search_scope_unavailable"
                       "Vector search is unavailable until tenant filtering is supported")))
  body)

(defroute api-data-op-post! []
  "POST" "/api/data/op/*"
  (let [path (aget request "params" "*")
        body (authorize-openplanner-proxy-post!
              ctx path (aget request "body"))]
    (send-openplanner-v1-json! config reply "POST" path body)))

(defroute api-data-op-delete! []
  "DELETE" "/api/data/op/*"
  (let [path (aget request "params" "*")]
    (send-openplanner-v1-json! config reply "DELETE" path nil)))

(defroute api-data-op-patch! []
  "PATCH" "/api/data/op/*"
  (let [path (aget request "params" "*")
        body (aget request "body")]
    (send-openplanner-v1-json! config reply "PATCH" path body)))

(defroute api-data-health! []
  "GET" "/api/data/health"
  (send-data-health! config reply))

(defroute api-data-mongo-collections! []
  "GET" "/api/data/mongo/collections"
  (send-mongo-collections! config reply))

(defroute api-data-mongo-list! []
  "GET" "/api/data/mongo/list"
  (send-json-promise! reply (openplanner-client/mongo-collections! (openplanner-client/client config))))

(def data-mongo-read-permission
  "org.datalakes.read")

(defn authorize-mongo-query!
  "Authorize raw data exploration and tenant-scope publication source events.

   Policy-disabled local mode is an explicitly trusted workstation boundary.
   Once an authenticated context exists, however, even a caller-supplied
   `extra.org_id` is replaced with the server-derived tenant before the query
   reaches OpenPlanner. Other collections retain their established query shape;
   this predicate closes the source-event disclosure introduced by document
   admission without guessing at unrelated collection schemas."
  [ctx ensure-permission-fn body]
  (if-not ctx
    body
    (do
      (ensure-permission-fn ctx data-mongo-read-permission)
      (if-not (= "events" (:collection body))
        body
        (let [org-id (some-> (ctx-org-id ctx) str str/trim not-empty)]
          (when-not org-id
            (throw (http-error 403 "org_scope_denied"
                               "Event queries require an authenticated organization")))
          (assoc body :filter
                 (assoc (or (:filter body) {}) :extra.org_id org-id)))))))

(defroute api-data-mongo-query! []
  "POST" "/api/data/mongo/query"
  (let [body (authorize-mongo-query! ctx ensure-permission!
                                     (request-body request))]
    (send-json-promise! reply (openplanner-client/mongo-query! (openplanner-client/client config) body))))

(defroute api-data-pg-tables! []
  "GET" "/api/data/pg/tables"
  ;; PostgreSQL was removed in the E14 Mongo migration (kanban 14-05).
  ;; The Mongo explorer at /api/data/mongo/* is the replacement surface.
  (json-response! reply 410 {:error "pg_removed"
                             :detail "PostgreSQL backend removed; use /api/data/mongo/collections"}))

(defroute api-data-jobs-build-semantic-edges! []
  "POST" "/api/data/jobs/build-semantic-edges"
  (let [body (request-body request)
        k (or (aget body "k") 8)
        min-sim (or (aget body "minSimilarity") 0.3)]
    (send-json-promise! reply
                        (openplanner-client/build-semantic-edges!
                         (openplanner-client/client config)
                         {:k k :minSimilarity min-sim}))))

(defroute api-data-pg-query! []
  "POST" "/api/data/pg/query"
  ;; PostgreSQL was removed in the E14 Mongo migration (kanban 14-05).
  (json-response! reply 410 {:error "pg_removed"
                             :detail "PostgreSQL backend removed; use /api/data/mongo/collections"}))

(defroute api-data-browse! []
  "GET" "/api/data/browse"
  (let [qs (aget request "query")
        path (or (aget qs "path") "")
        ingestion-base (:ingestion-base-url config)
        target-url (str ingestion-base "/api/ingestion/browse" (if (str/blank? path) "" (str "?path=" (js/encodeURIComponent path))))]
    (send-data-browse! reply target-url)))

(defroute api-data-file! []
  "GET" "/api/data/file"
  (let [qs (aget request "query")
        path (or (aget qs "path") "")
        ingestion-base (:ingestion-base-url config)]
    (send-fetch-json! reply
                      (str ingestion-base "/api/ingestion/file?path=" (js/encodeURIComponent path))
                      nil
                      fetch-json-err)))

(defroute api-data-graphql! []
  "POST" "/api/data/graphql"
  (let [body (request-body request)
        gw-url "http://127.0.0.1:8796/graphql"]
    (send-fetch-json! reply
                      gw-url
                      {:method "POST"
                       :headers {"Content-Type" "application/json"}
                       :body (or (some-> body js/JSON.stringify) "{}")}
                      fetch-json-err)))

(defroute api-data-graph-status! []
  "GET" "/api/data/graph/status"
  (send-fetch-json! reply "http://127.0.0.1:8796/api/status" {:method "GET"} fetch-json-err))

;; Embed the graph-weaver WebGL view URL for the frontend
(defroute api-data-graph-view-url! []
  "GET" "/api/data/graph/view-url"
  (json-response! reply 200 {:url "http://127.0.0.1:8796"}))

(defroute api-knoxx-health! []
  "GET" "/api/knoxx/health"
  (send-knoxx-health! config reply))

(defn- chat-turn-ok [reply resp]
  (json-response! reply 200 resp))

(defn- chat-turn-err [reply err]
  (error-response! reply err 502))

(defroute api-knoxx-chat! []
  "POST" "/api/knoxx/chat"
  (when ctx (ensure-permission! ctx "agent.chat.use"))
  (let [parsed0 (normalize-chat-body (request-body request))
        parsed (assoc parsed0 :agent-spec (merged-agent-spec config parsed0))
        agent-ctx (effective-auth-context ctx parsed)
        body (assoc parsed
                    :mode "rag"
                    :auth-context agent-ctx)]
    (try
      (chat-turn-ok reply (await (send-agent-turn! runtime config body)))
      (catch :default err
        (chat-turn-err reply err)))))

(defroute api-knoxx-chat-start! []
  "POST" "/api/knoxx/chat/start"
  (when ctx
    (ensure-permission! ctx "agent.chat.use"))
  (handle-chat-start runtime config reply ctx request))

(defroute api-knoxx-direct! []
  "POST" "/api/knoxx/direct"
  (when ctx (ensure-permission! ctx "agent.chat.use"))
  (let [parsed0 (normalize-chat-body (request-body request))
        parsed (assoc parsed0 :agent-spec (merged-agent-spec config parsed0))
        agent-ctx (effective-auth-context ctx parsed)
        body (assoc parsed
                    :mode "direct"
                    :auth-context agent-ctx)]
    (try
      (chat-turn-ok reply (await (send-agent-turn! runtime config body)))
      (catch :default err
        (chat-turn-err reply err)))))

(defroute api-knoxx-direct-start! []
  "POST" "/api/knoxx/direct/start"
  (when ctx (ensure-permission! ctx "agent.chat.use"))
  (handle-direct-start runtime config reply ctx request))

(defn- steer-ok [reply resp]
  (json-response! reply 200 resp))

(defn- steer-err [reply err]
  (error-response! reply err 409))

(defroute api-knoxx-steer! []
  "POST" "/api/knoxx/steer"
  (when ctx (ensure-permission! ctx "agent.controls.steer"))
  (let [body (assoc (normalize-control-body (request-body request)) :kind "steer")
        actor-ctx (auth-context-with-actor ctx (:actor-id body))]
    (ensure-conversation-access! actor-ctx (:conversation-id body))
    (try
      (steer-ok reply (await (queue-agent-control! runtime config body)))
      (catch :default err
        (steer-err reply err)))))

(defroute api-knoxx-follow-up! []
  "POST" "/api/knoxx/follow-up"
  (when ctx (ensure-permission! ctx "agent.controls.follow_up"))
  (let [body (assoc (normalize-control-body (request-body request)) :kind "follow_up")
        actor-ctx (auth-context-with-actor ctx (:actor-id body))]
    (ensure-conversation-access! actor-ctx (:conversation-id body))
    (try
      (steer-ok reply (await (queue-agent-control! runtime config body)))
      (catch :default err
        (steer-err reply err)))))

(defn- abort-ok [reply resp]
  (json-response! reply (if (:ok resp) 200 409) resp))

;; Abort / interrupt the current running turn for a conversation.
;; This is stronger than steer(): it cancels the current operation immediately.
(defroute api-knoxx-abort! []
  "POST" "/api/knoxx/abort"
  (when ctx (ensure-permission! ctx "agent.controls.steer"))
  (let [raw (request-body request)
        conversation-id (or (aget raw "conversation_id") (aget raw "conversationId") "")
        actor-id (or (aget raw "actor_id") (aget raw "actorId") (aget raw "actor-id"))
        actor-ctx (auth-context-with-actor ctx actor-id)
        reason (or (aget raw "reason") "aborted_by_user")]
    (if (str/blank? (str conversation-id))
      (json-response! reply 400 {:ok false :error "conversation_id is required"})
      (do
        (ensure-conversation-access! actor-ctx conversation-id)
        (try
          (abort-ok reply (await (turn-control/abort-active-turn! conversation-id reason)))
          (catch :default err
            (steer-err reply err)))))))

(defroute api-knoxx-session-undo! []
  "POST" "/api/knoxx/session/undo"
  (when ctx (ensure-permission! ctx "agent.chat.use"))
  (let [raw (request-body request)
        session-id (str (or (aget raw "session_id")
                            (aget raw "sessionId")
                            ""))
        provided-conversation-id (str (or (aget raw "conversation_id")
                                          (aget raw "conversationId")
                                          ""))
        actor-id (or (aget raw "actor_id") (aget raw "actorId") (aget raw "actor-id"))
        actor-ctx (auth-context-with-actor ctx actor-id)
        turns-raw (or (aget raw "turns") 1)
        turns (let [parsed (js/parseInt (str turns-raw) 10)]
                (if (js/isNaN parsed) 1 (max 1 parsed)))]
    (if (str/blank? session-id)
      (json-response! reply 400 {:ok false :error "session_id is required"})
      (try
        (let [session (await (session-store/get-session session-id))]
          (cond
            (nil? session)
            (json-response! reply 404 {:ok false :error "Session not found or expired"})

            (= "running" (:status session))
            (json-response! reply 409 {:ok false :error "Cannot undo while a turn is still running"})

            :else
            (let [conversation-id (str (or (:conversation_id session) provided-conversation-id ""))
                  current-messages (vec (or (:messages session) []))
                  rewound-messages (session-store/rewind-messages current-messages turns)
                  removed-count (- (count current-messages) (count rewound-messages))]
              (when (and actor-ctx (not (str/blank? conversation-id)))
                (ensure-conversation-access! actor-ctx conversation-id))
              (if (zero? removed-count)
                (json-response! reply 409 {:ok false :error "No user turns available to undo"})
                (do
                  (await (session-store/undo-session-turns! session-id turns))
                  (json-response! reply 200 {:ok true
                                             :session_id session-id
                                             :conversation_id conversation-id
                                             :removed_count removed-count
                                             :remaining_messages (count rewound-messages)}))))))
        (catch :default err
          (undo-session-err reply err))))))


(defn- build-active-runs [ctx limit]
  (let [sessions-by-id (into {}
                             (map (fn [session]
                                    [(:session_id session) session]))
                             (session-store/active-session-snapshots))]
    (->> @run-order*
         (map #(get @runs* %))
         (filter some?)
         (filter #(contains? #{"queued" "running" "waiting_input"} (:status %)))
         (filter #(run-visible? ctx %))
         (map (fn [run]
                (active-run-summary run (get sessions-by-id (:session_id run)))))
         (take limit)
         vec)))

(defroute api-knoxx-agents-active! []
  "GET" "/api/knoxx/agents/active"
  (when ctx (ensure-permission! ctx "agent.chat.use"))
  (let [limit-raw (aget request "query" "limit")
        limit (if (string? limit-raw)
                (max 1 (js/parseInt limit-raw 10))
                25)
        items (build-active-runs ctx limit)]
    (agents-active-ok reply items)))

(defroute api-admin-agents-active! []
  "GET" "/api/admin/agents/active"
  (ensure-permission! ctx "org.events.control")
  (let [limit-raw (aget request "query" "limit")
        limit (or (parse-positive-int limit-raw) 200)]
    (try
      (agents-active-ok reply (await (live-active-agent-summaries! limit false)))
      (catch :default err
        (agents-active-err reply err)))))

(defroute api-admin-agents-abort! []
  "POST" "/api/admin/agents/abort"
  (ensure-permission! ctx "org.events.control")
  (handle-admin-abort reply ctx request))

;; Session status endpoint for frontend resume detection
(defroute api-knoxx-session-status! []
  "GET" "/api/knoxx/session/status"
  (handle-session-status runtime config reply request))

;; Run event catch-up endpoint for WS reconnect recovery
(defroute api-knoxx-run-events! []
  "GET" "/api/knoxx/run/:runId/events"
  (when ctx (ensure-permission! ctx "agent.chat.use"))
  (let [run-id (aget request "params" "runId")
        since (or (aget request "query" "since") "")]
    (if (str/blank? run-id)
      (json-response! reply 400 {:error "runId is required"})
      (try
        (run-events-ok reply run-id (await (run-state/get-run-events-since run-id since)))
        (catch :default err
          (run-events-err reply err))))))

(defroute api-knoxx-run-get! []
  "GET" "/api/knoxx/runs/:runId"
  (when ctx (ensure-permission! ctx "agent.chat.use"))
  (let [run-id (str (or (aget request "params" "runId") ""))]
    (cond
      (str/blank? run-id)
      (json-response! reply 400 {:error "runId required"})

      (some? (get @runs* run-id))
      (if-let [filtered (run-visible? ctx (get @runs* run-id))]
        (json-response! reply 200
                        {:ok true :source "memory" :run filtered})
        (json-response! reply 403 {:error "Access denied"}))

      :else
      (json-response! reply 404 {:ok false :error "Run not found"
                                 :run_id run-id}))))

(defroute api-shibboleth-handoff! []
  "POST" "/api/shibboleth/handoff"
  (let [body (request-body request)]
    (if (str/blank? (:shibboleth-base-url config))
      (json-response! reply 503 {:detail "SHIBBOLETH_BASE_URL is not configured"})
      (let [payload {:source_app "knoxx"
                     :model (:model body)
                     :system_prompt (:system_prompt body)
                     :provider (:provider body)
                     :conversation_id (:conversation_id body)
                     :fake_tools_enabled (boolean (:fake_tools_enabled body))
                     :items (or (:items body) [])}]
        (try
          (let [resp (await (fetch-json (str (:shibboleth-base-url config) "/api/chat/import")
                                        {:method "POST"
                                         :json payload}))]
            (if (:ok resp)
              (shibboleth-ok config reply request body (:body resp))
              (shibboleth-import-failed reply resp)))
          (catch :default err
            (shibboleth-unreachable reply err)))))))

(defn- register-core-routes!
  [app runtime config]
  (health! app runtime config deps)
  (dev-hmr! app runtime config deps)
  (config! app runtime config deps)
  (api-knoxx-agents-catalog! app runtime config deps)
  (api-auth-context! app runtime config deps))

(defn- register-admin-and-memory-routes!
  [app runtime config lounge-messages*]
  (admin-routes/register-admin-routes! app runtime
                                       {:route! route!
                                        :json-response! json-response!
                                        :with-request-context! with-request-context!
                                        :ensure-permission! ensure-permission!
                                        :ensure-any-permission! ensure-any-permission!
                                        :ensure-org-scope! ensure-org-scope!
                                        :policy-db policy-db
                                        :policy-db-promise policy-db-promise
                                        :http-error http-error})
  (memory-routes/register-memory-routes! app runtime config
                                         {:route! route!
                                          :json-response! json-response!
                                          :error-response! error-response!
                                          :with-request-context! with-request-context!
                                          :ensure-permission! ensure-permission!
                                          :parse-positive-int parse-positive-int
                                          :truthy-param? truthy-param?
                                          :start-session-title-backfill! start-session-title-backfill!
                                          :session-title-backfill* session-title-backfill*
                                          :session-titles* session-titles*
                                          :get-cached-session-title! get-cached-session-title!
                                          :fetch-openplanner-session-rows! fetch-openplanner-session-rows!
                                          :session-title-seed-text session-title-seed-text
                                          :heuristic-session-title heuristic-session-title
                                          :stored-session-title-entry stored-session-title-entry
                                          :cache-session-title-entry! cache-session-title-entry!
                                          :resolve-session-title! resolve-session-title!
                                          :cache-session-title! cache-session-title!
                                          :normalize-session-title normalize-session-title
                                          :session-visible? session-visible?
                                          :session-matches-page-actor-filter? session-matches-page-actor-filter?
                                          :openplanner-memory-search! openplanner-memory-search!
                                          :filter-authorized-memory-hits! filter-authorized-memory-hits!
                                          :ctx-permitted? ctx-permitted?
                                          :system-admin? system-admin?
                                          :http-error http-error
                                          :now-iso now-iso
                                          :broadcast-ws! broadcast-ws!
                                          :lounge-messages* lounge-messages*
                                          :authorized-session-ids! authorized-session-ids!}))

(defn- register-tooling-route-groups!
  [app runtime config]
  (let [session-guard          (guards/make-session-guard runtime)
        optional-session-guard (guards/make-optional-session-guard runtime)]
    (tool-routes/register-tool-routes! app runtime config
                                       {:route! route!
                                        :json-response! json-response!
                                        :error-response! error-response!
                                        :with-request-context! with-request-context!
                                        :ensure-permission! ensure-permission!
                                        :tool-catalog tool-catalog
                                        :ensure-role-can-use! ensure-role-can-use!
                                        :resolve-workspace-path resolve-workspace-path
                                        :count-occurrences count-occurrences
                                        :replace-first replace-first
                                        :clip-text clip-text
                                        :session-guard session-guard
                                        :optional-session-guard optional-session-guard})
    (actor-routes/register-actor-routes! app runtime config
                                         {:route! route!
                                          :json-response! json-response!
                                          :error-response! error-response!
                                          :with-request-context! with-request-context!
                                          :ensure-permission! ensure-permission!
                                          :session-guard session-guard})))

(defn- ^:async repair-publication-translation-events!
  [client scope]
  (let [evidence-store (translation-evidence-registry/current)
        split-store (translation-split-registry/current)]
    (when-not evidence-store
      (throw (ex-info "translation evidence persistence is not configured"
                      {:status 503
                       :code "translation_evidence_unavailable"})))
    (when-not split-store
      (throw (ex-info "translation split persistence is not configured"
                      {:status 503
                       :code "translation_split_persistence_unavailable"})))
    (await
     (translation-event-writer/repair-completed-event-projections!
      {:evidence-store evidence-store
       :openplanner-client client
       :split-store split-store}
      (select-keys scope [:org-id :project])))))

(defn- register-publication-surface-routes!
  "The contract-owned publication surface: the resource projection, the CMS
   editor's view of it, and translation configuration. None of these is gated on
   a hosted publishing backend being reachable — resolving desired state with
   that backend absent is the whole point."
  [app runtime config]
  (let [client (openplanner-client/client config)
        helpers {:route! route!
                 :json-response! json-response!
                 :with-request-context! with-request-context!
                 :ensure-permission! ensure-permission!}
        admission-dependencies
        {:client client
         :repair-translation-events!
         (partial repair-publication-translation-events! client)}
        internal-admission!
        (fn [scope selection]
          (document-admission/admit-documents!
           config
           (assoc admission-dependencies
                  :dispatch-document!
                  (fn [document-id snapshot-deps]
                    (translation-dispatch-routes/dispatch-selection-for-scope!
                     config scope {:document document-id}
                     (assoc snapshot-deps :client client))))
           scope selection))]
    ;; Generated draft tools call this cycle-free port. The handler derives all
    ;; model work from the server-pinned scope carried on the originating
    ;; admission event; no HTTP credential or agent-supplied identity is used.
    (publication-admission-hook/register! internal-admission!)
    ;; Fastify interop is owned by each extern adapter, which authorizes before
    ;; touching the filesystem-backed projection.
    (publication-routes/register-publication-routes!
     app runtime config (select-keys helpers [:with-request-context!
                                              :ensure-permission!]))
    (cms-publication-routes/register-cms-publication-routes! app runtime config helpers)
    (cms-document-routes/register! app runtime helpers)
    (reconcile-routes/register-publication-reconcile-routes!
     app runtime config (select-keys helpers [:with-request-context!
                                              :ensure-permission!]))
    (translation-config-routes/register-translation-config-routes!
     app runtime config helpers)
    (document-admission-routes/register-document-admission-routes!
     app runtime config (select-keys helpers [:with-request-context!
                                              :ensure-permission!])
     admission-dependencies)
    (translation-dispatch-routes/register-translation-dispatch-routes!
     app runtime config (select-keys helpers [:with-request-context!
                                              :ensure-permission!]))
    (translation-review-routes/register-translation-review-routes!
     app runtime config (select-keys helpers [:with-request-context!
                                              :ensure-permission!]))))

(defn- register-resource-and-media-routes!
  [app runtime config]
  (resource-routes/register-resource-routes! app runtime config
                                             {:route! route!
                                              :json-response! json-response!
                                              :error-response! error-response!
                                              :with-request-context! with-request-context!
                                              :ensure-permission! ensure-permission!})
  (register-publication-surface-routes! app runtime config)
  (model-routes/register-model-routes! app runtime config)
  (voice-routes/register-voice-routes! app runtime config
                                       {:route! route!
                                        :json-response! json-response!
                                        :with-request-context! with-request-context!
                                        :ensure-tool! ensure-tool!})
  (document-routes/register-document-routes! app runtime config
                                             {:route! route!
                                              :json-response! json-response!
                                              :error-response! error-response!
                                              :with-request-context! with-request-context!
                                              :ensure-permission! ensure-permission!
                                              :clip-text clip-text
                                              :openplanner-graph-export! openplanner-graph-export!
                                              :send-fetch-response! send-fetch-response!
                                              :bearer-headers bearer-headers
                                              :fetch-json fetch-json
                                              :openai-auth-error openai-auth-error
                                              :request-query-string request-query-string})
  (workspace-media-routes/register-workspace-media-routes! app runtime config
                                                           {:route! route!
                                                            :json-response! json-response!
                                                            :error-response! error-response!
                                                            :with-request-context! with-request-context!
                                                            :ensure-tool! ensure-tool!})
  (studio-routes/register-studio-routes! app runtime config
                                         {:route! route!
                                          :json-response! json-response!
                                          :error-response! error-response!
                                          :with-request-context! with-request-context!
                                          :ensure-tool! ensure-tool!
                                          :ensure-permission! ensure-permission!
                                          :policy-db policy-db
                                          :policy-db-promise policy-db-promise}))

(defn- register-ingestion-routes!
  [app runtime config]
  (api-knoxx-proxy-get! app runtime config deps)
  (api-knoxx-proxy-post! app runtime config deps)
  (api-knoxx-proxy-put! app runtime config deps)
  (api-knoxx-proxy-patch! app runtime config deps)
  (api-knoxx-proxy-delete! app runtime config deps)
  (api-ingestion-browse! app runtime config deps)
  (api-ingestion-file! app runtime config deps)
  (api-ingestion-sources! app runtime config deps)
  (api-ingestion-jobs-get! app runtime config deps)
  (api-ingestion-jobs-post! app runtime config deps)
  (api-ingestion-proxy-get! app runtime config deps)
  (api-ingestion-proxy-post! app runtime config deps)
  (api-ingestion-proxy-delete! app runtime config deps))

(defn- register-data-routes!
  [app runtime config]
  (api-data-op-get! app runtime config deps)
  (api-data-op-post! app runtime config deps)
  (api-data-op-patch! app runtime config deps)
  (api-data-op-delete! app runtime config deps)
  (api-data-health! app runtime config deps)
  (api-data-mongo-collections! app runtime config deps)
  (api-data-mongo-list! app runtime config deps)
  (api-data-mongo-query! app runtime config deps)
  (api-data-pg-tables! app runtime config deps)
  (api-data-jobs-build-semantic-edges! app runtime config deps)
  (api-data-pg-query! app runtime config deps)
  (api-data-browse! app runtime config deps)
  (api-data-file! app runtime config deps)
  (api-data-graphql! app runtime config deps)
  (api-data-graph-status! app runtime config deps)
  (api-data-graph-view-url! app runtime config deps))

(defn- register-knoxx-run-routes!
  [app runtime config]
  (api-knoxx-health! app runtime config deps)
  (api-knoxx-chat! app runtime config deps)
  (api-knoxx-chat-start! app runtime config deps)
  (api-knoxx-direct! app runtime config deps)
  (api-knoxx-direct-start! app runtime config deps)
  (api-knoxx-steer! app runtime config deps)
  (api-knoxx-follow-up! app runtime config deps)
  (api-knoxx-abort! app runtime config deps)
  (api-knoxx-session-undo! app runtime config deps)
  (api-knoxx-agents-active! app runtime config deps)
  (api-admin-agents-active! app runtime config deps)
  (api-admin-agents-abort! app runtime config deps)
  (api-knoxx-session-status! app runtime config deps)
  (api-knoxx-run-events! app runtime config deps)
  (api-knoxx-run-get! app runtime config deps)
  (api-shibboleth-handoff! app runtime config deps))

(defn- register-translation-route-group!
  [app runtime config]
  (translation-routes/register-translation-routes! app runtime config
                                                   {:json-response! json-response!
                                                    :error-response! error-response!
                                                    :with-request-context! with-request-context!
                                                    :ensure-permission! ensure-permission!
                                                    :ctx-user-id ctx-user-id
                                                    :ctx-user-email ctx-user-email
                                                    :ctx-org-id ctx-org-id}))

(defn register-routes!
  [runtime app config lounge-messages*]
  (ensure-settings! config)
  (register-core-routes! app runtime config)
  (register-admin-and-memory-routes! app runtime config lounge-messages*)
  (register-tooling-route-groups! app runtime config)
  (register-resource-and-media-routes! app runtime config)
  (register-ingestion-routes! app runtime config)
  (register-data-routes! app runtime config)
  (register-knoxx-run-routes! app runtime config)
  (register-translation-route-group! app runtime config))
