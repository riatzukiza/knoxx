(ns knoxx.backend.infra.agent.turn
  "Main turn orchestrator: send-agent-turn! and supporting lifecycle functions."
  (:require [clojure.string :as str]
            [knoxx.backend.infra.run-event-payload :as run-payload]
            [knoxx.backend.infra.agent.initial-admission :as initial-admission]
            [knoxx.backend.infra.agent.turn-startup :as startup]
            [knoxx.backend.infra.character.turn-context :as character-context]
            [knoxx.backend.infra.agent.hydration :as hydration :refer [settings-state* ensure-settings!
                                                                passive-hydration! passive-memory-hydration!
                                                                build-agent-user-message
                                                                hydration-sources]]
            [knoxx.backend.infra.agent.session :refer [ensure-agent-session!]]
            [knoxx.backend.infra.agent.message :as msg]
            [knoxx.backend.extern.agent-turn-media :as xturn-media]
            [knoxx.backend.extern.agent-turn-node :as xturn-node]
            [knoxx.backend.extern.agent-turn-prompt :as xturn-prompt]
            [knoxx.backend.extern.agent-turn-result :as xturn-result]
            [knoxx.backend.extern.promise :as xpromise]
            [knoxx.backend.domain.agent.content :as content :refer [merge-content-parts]]
            [knoxx.backend.domain.error-observatory :as errors]
            [knoxx.backend.infra.agent.policy :as policy]
            [knoxx.backend.infra.agent.stream :as stream]
            [knoxx.backend.infra.agent.transcript :as transcript]
            [knoxx.backend.infra.agent.turn-finalization :as finalization]
            [knoxx.backend.infra.auth.authz :as authz :refer [auth-snapshot]]
            [knoxx.backend.infra.core-memory :refer [extract-mentioned-devel-paths extract-mentioned-urls]]
            [knoxx.backend.infra.clients.openplanner :as openplanner-client]
            [knoxx.backend.infra.openplanner.memory :as openplanner-memory]
            [knoxx.backend.domain.media :as media]
            [knoxx.backend.domain.realtime :refer [broadcast-ws-session!]]
            [knoxx.backend.domain.action.run-state :refer [append-run-event! update-run!
                                                           finalize-run-trace-blocks!
                                                           record-retrieval-sample! latest-assistant-message
                                                           set-event-stream-sink!]]
            [knoxx.backend.domain.models :refer [effective-thinking-level normalize-thinking-level model-supports-input?]]
            [knoxx.backend.shape.agent :refer [send-user-message! subscribe!]]
            [knoxx.backend.infra.stores.mongo-session-store :as session-store]
            [knoxx.backend.infra.run-events :as run-events]
            [knoxx.backend.infra.stores.session-titles :refer [maybe-prime-session-title!]]
            [knoxx.backend.domain.text :refer [assistant-message-text assistant-message-reasoning-text]]
            [knoxx.backend.domain.voice.turn-control :as turn-control]
            [knoxx.backend.domain.agent.agent-context :as agent-ctx]
            [knoxx.backend.domain.character.decision-input :as decision-input]
            [knoxx.backend.domain.time :refer [now-iso]]))

(defonce conversation-access* (atom {}))
(defonce lounge-messages* (atom []))


(defn ensure-conversation-access!
  [ctx conversation-id]
  (authz/ensure-conversation-access! conversation-access* ctx conversation-id))

(defn remember-conversation-access!
  [ctx conversation-id]
  (authz/remember-conversation-access! conversation-access* ctx conversation-id))

(defn- auth-context-for-agent-turn
  "Preserve an inbound server principal and its caller-specific policies.
   Only context-free legacy turns synthesize role/tool/resource ceilings from
   the spec. That synthesized actor label cannot authorize character scope;
   trusted scheduled character starts supply their own in-process context."
  [auth-context agent-spec]
  (let [agent-actor-id (some-> (:actor-id agent-spec) str str/trim not-empty)
        needs-context? (or auth-context
                           agent-actor-id
                           (seq (:tool-policies agent-spec))
                           (seq (:resource-policies agent-spec))
                           (:role agent-spec))]
    (when needs-context?
      (cond-> (or auth-context {})
        ;; Preserve the inbound server principal, including a genuine event token.
        ;; A requested spec cannot relabel that token as a different actor.
        (and (nil? auth-context) agent-actor-id) (assoc :actorId agent-actor-id)
        (and (nil? auth-context) (seq (:tool-policies agent-spec)))
        (assoc :toolPolicies (vec (:tool-policies agent-spec)))
        (and (nil? auth-context) (seq (:resource-policies agent-spec)))
        (assoc :resourcePolicies (:resource-policies agent-spec))
        (and (nil? auth-context) (:role agent-spec))
        (assoc :roleSlugs [(:role agent-spec)])))))

(defn ensure-session-id
  [session-id]
  (or (content/nonblank session-id)
      (xturn-node/random-uuid!)))



(defn- install-openplanner-event-sink!
  [config]
  (set-event-stream-sink!
     (^:async fn [event]
       (let [client (openplanner-client/client config)]
         (when (openplanner-client/enabled? client)
           (try
             (await (openplanner-client/events!
                     client
                     [(openplanner-memory/openplanner-event
                       config
                       {:id        (str (:run_id event) ":"
                                        (:type event) ":"
                                        (:at event))
                        :ts        (:at event)
                        :kind      (str "knoxx." (:type event))
                        ;; Session-scoped diagnostics must carry the session project:
                        ;; without it they default to the workspace project and the
                        ;; /v1/sessions list (filtered by session project) cannot see
                        ;; in-flight threads until their first run completes.
                        :project   (:session-project-name config)
                        :session   (:conversation_id event)
                        :message   (:run_id event)
                        :role      "system"
                        :text      (str (:type event)
                                        (when (:tool_name event)
                                          (str ": " (:tool_name event)))
                                        (when (:preview event)
                                          (str "\n" (:preview event))))
                        :extra     event})]))
             (catch :default _ nil)))))))

(defn- extract-turn-answer-and-reasoning
  "Extract the final answer and reasoning text from streaming state and assistant message."
  [state assistant-message]
  (let [answer (let [chunked (apply str @(:chunks state))]
                 (if (str/blank? chunked)
                   (assistant-message-text assistant-message)
                   chunked))
        reasoning-text (let [streamed (apply str @(:reasoning-chunks state))
                             final-reasoning (assistant-message-reasoning-text assistant-message)]
                         (cond
                           (and (str/blank? streamed) (not (str/blank? final-reasoning))) final-reasoning
                           (and (not (str/blank? final-reasoning)) (> (count final-reasoning) (count streamed))) final-reasoning
                           :else streamed))
        think-split (when (str/blank? (str reasoning-text))
                      (msg/split-think-tags answer))
        answer (if (and think-split (:hadThinkTags think-split))
                 (:answer think-split)
                 answer)
        reasoning-text (if (and think-split (:hadThinkTags think-split))
                         (:reasoning think-split)
                         reasoning-text)]
    {:answer answer
     :reasoning-text reasoning-text}))

(defn- build-turn-completed-response
  "Build the final response map for a completed turn."
  [run-id conversation-id session-id model-id answer merged-content-parts sources message-parts]
  {:answer answer
   :run_id run-id
   :runId run-id
   :conversation_id conversation-id
   :conversationId conversation-id
   :session_id session-id
   :model model-id
   :content_parts merged-content-parts
   :sources sources
   :message_parts message-parts
   :compare nil})

(defn- finalize-run-record!
  [run-id answer reasoning-text sources elapsed usage-tokens assistant-content-parts hydration memory-hydration]
  (update-run! run-id
               (fn [run]
                 (let [resource-patch (cond-> {:sources sources}
                                        hydration (assoc :passiveHydration (select-keys hydration [:query :tokens :database :elapsedMs :results]))
                                        memory-hydration (assoc :memoryHydration (hydration/memory-hydration-projection memory-hydration)))
                       merged-content-parts (merge-content-parts assistant-content-parts
                                                                 (content/reply-attachment-content-parts (:tool_receipts run)))]
                   (-> run
                       (assoc :updated_at (now-iso)
                              :status "completed"
                              :total_time_ms elapsed
                              :input_tokens (:input-tokens usage-tokens)
                              :output_tokens (:output-tokens usage-tokens)
                              :tokens_per_s (when (and (pos? (:output-tokens usage-tokens)) (pos? elapsed))
                                              (* 1000 (/ (:output-tokens usage-tokens) elapsed)))
                              :answer answer
                              :content_parts merged-content-parts
                              :reasoning reasoning-text
                              :sources sources)
                       (update :resources merge resource-patch))))))

(defn- empty-turn-output?
  [answer completed-run merged-content-parts]
  (and (str/blank? (str answer))
       (empty? (or (:tool_receipts completed-run) []))
       (empty? (or merged-content-parts []))))

(defn- normalize-tools-choice
  [tools-choice]
  (when (some? tools-choice)
    (-> (if (keyword? tools-choice)
          (name tools-choice)
          (str tools-choice))
        str/trim
        str/lower-case)))

(defn- turn-output-failure
  [answer completed-run merged-content-parts agent-spec]
  (cond
    (and (= "required-first" (normalize-tools-choice (:tools-choice agent-spec)))
         (empty? (or (:tool_receipts completed-run) [])))
    {:diagnostic-type :agent-turn/required-tool-not-called
     :message "Agent turn completed without calling a tool required by tools-choice"
     :reason "required_tool_not_called"}

    (empty-turn-output? answer completed-run merged-content-parts)
    {:diagnostic-type :agent-turn/empty-output
     :message "Agent turn completed without assistant text, tool calls, or content parts"
     :reason "empty_output"}

    :else nil))

(defn- ^:async finalize-refused-turn-output!
  [config session run-id conversation-id session-id started-ms model-id persisted-request-messages agent-spec completed-run merged-content-parts output-failure event-stream-sink]
  (let [{:keys [diagnostic-type message reason]} output-failure
        err (js/Error. message)
        diagnostic (finalization/refusal-diagnostic!
                    {:run-id run-id :conversation-id conversation-id :session-id session-id :model model-id}
                    agent-spec diagnostic-type err)
        err-text (:message diagnostic)
        failed-event (run-payload/tool-event-payload run-id conversation-id session-id "run_failed"
                                         {:status "failed"
                                          :error err-text
                                          :reason reason})
        failed-run (finalization/mark-failed!
                    run-id {:total_time_ms (- (.now js/Date) started-ms)
                            :error err-text :reason reason})]
    (append-run-event! run-id failed-event)
    (await (finalization/settle!
            {:run-id run-id :conversation-id conversation-id :session-id session-id
             :event-stream-sink event-stream-sink}
            (^:async fn []
              (when failed-run
                (await (openplanner-memory/index-run-memory! config failed-run extract-mentioned-devel-paths extract-mentioned-urls)))
              (broadcast-ws-session! session-id "events" failed-event))
            #(finalization/complete! session agent-spec session-id conversation-id
                                    {:status "failed" :error err-text} persisted-request-messages)))
    (assoc (build-turn-completed-response run-id conversation-id session-id model-id ""
                                          merged-content-parts (:sources completed-run) [])
           :error err-text)))

(defn- ^:async finalize-accepted-turn-output!
  [config session run-id conversation-id session-id model-id answer reasoning-text
   sources message-parts elapsed usage-tokens assistant-content-parts hydration
   memory-hydration persisted-request-messages agent-spec completed-event event-stream-sink]
  (finalize-run-trace-blocks! run-id "done")
  (let [completed-run
        (finalize-run-record!
         run-id answer reasoning-text sources elapsed usage-tokens
         assistant-content-parts hydration memory-hydration)
        merged-content-parts
        (vec (or (:content_parts completed-run) assistant-content-parts))
        response
        (build-turn-completed-response
         run-id conversation-id session-id model-id answer
         merged-content-parts sources message-parts)]
    (append-run-event! run-id completed-event)
    (await (finalization/settle!
            {:run-id run-id :conversation-id conversation-id :session-id session-id
             :event-stream-sink event-stream-sink}
            (^:async fn []
              (when completed-run
                (await (openplanner-memory/index-run-memory!
                        config completed-run extract-mentioned-devel-paths extract-mentioned-urls)))
              (broadcast-ws-session! session-id "events" completed-event))
            #(finalization/complete!
              session agent-spec session-id conversation-id {:status "completed" :answer answer}
              (conj persisted-request-messages
                    (cond-> {:role "assistant" :content answer}
                      (seq merged-content-parts) (assoc :content-parts merged-content-parts))))))
    response))

(defn- ^:async finalize-turn-success!
  [config state session run-id conversation-id session-id started-ms model-id _mode
   hydration memory-hydration persisted-request-messages agent-spec]
  (let [assistant-message (latest-assistant-message session)
        {:keys [answer reasoning-text]} (extract-turn-answer-and-reasoning state assistant-message)
        assistant-content-parts (content/assistant-content-parts assistant-message)
        usage-tokens (xturn-result/usage-tokens assistant-message)
        elapsed (- (.now js/Date) started-ms)
        output-tokens (:output-tokens usage-tokens)
        _tokens-per-second (when (and (pos? output-tokens) (pos? elapsed))
                            (* 1000 (/ output-tokens elapsed)))
        sources (hydration-sources hydration)
        message-parts (cond-> []
                        (not (str/blank? reasoning-text))
                        (conj {:role "thinking"
                               :content reasoning-text
                               :reasoningType "reasoning_summary"})
                        (not (str/blank? answer))
                        (conj {:role "assistant"
                               :content answer}))
        completed-event (run-payload/tool-event-payload run-id conversation-id session-id "run_completed"
                                            {:status "completed"
                                             :model model-id
                                             :sources_count (count sources)})]
    (record-retrieval-sample! (:retrievalMode @settings-state*) elapsed)
    ;; Check the tool obligation against the still-running record. Stamping the
    ;; run completed first would expose a false-success window and index the
    ;; provider's call-shaped prose as completed memory before the guard could
    ;; correct it to failed.
    (let [running-run (update-run! run-id identity)
          pending-content-parts
          (merge-content-parts
           assistant-content-parts
           (content/reply-attachment-content-parts (:tool_receipts running-run)))]
      (if-let [output-failure
               (turn-output-failure answer running-run pending-content-parts
                                    agent-spec)]
        (do
          (finalize-run-trace-blocks! run-id "error")
          (await (finalize-refused-turn-output!
                  config session run-id conversation-id session-id started-ms
                  model-id persisted-request-messages agent-spec running-run
                  pending-content-parts output-failure (:event-stream-sink state))))
        (await (finalize-accepted-turn-output!
                config session run-id conversation-id session-id model-id answer
                reasoning-text sources message-parts elapsed usage-tokens
                assistant-content-parts hydration memory-hydration
                persisted-request-messages agent-spec completed-event (:event-stream-sink state)))))))

(defn- fail-turn-record! [run-id payload passive memory]
  (when (finalization/fail-run! run-id payload nil nil)
    (update-run! run-id #(update % :resources merge (character-context/resource-patch passive memory)))))

(defn- ^:async finalize-turn-failure!
  [config state session run-id conversation-id session-id started-ms
   hydration memory-hydration persisted-request-messages agent-spec err]
  (let [err-text (or @(:abort-reason* state) (str err))
        error-event (run-payload/tool-event-payload run-id conversation-id session-id "run_failed"
                                        {:status "failed"
                                         :error err-text})]
    (finalize-run-trace-blocks! run-id "error")
    (let [failed-run (fail-turn-record!
                      run-id {:total_time_ms (- (.now js/Date) started-ms)
                              :reasoning (apply str @(:reasoning-chunks state)) :error err-text}
                      hydration memory-hydration)]
      (append-run-event! run-id error-event)
      (await (finalization/settle!
              {:run-id run-id :conversation-id conversation-id :session-id session-id
               :event-stream-sink (:event-stream-sink state)}
              (^:async fn []
                (when failed-run
                  (await (openplanner-memory/index-run-memory!
                          config failed-run extract-mentioned-devel-paths extract-mentioned-urls)))
                (broadcast-ws-session! session-id "events" error-event))
              #(finalization/complete! session agent-spec session-id conversation-id
                                      {:status "failed" :error err-text} persisted-request-messages))))
    (throw err)))
(defn content-part-type [part]
  (cond
    (keyword? (:type part)) (name (:type part))
    (string? (:type part)) (:type part)
    :else nil))
(defn data-url->image-attachment [raw]
  (when (and (string? raw) (str/starts-with? raw "data:"))
    (let [[meta b64] (str/split raw #"," 2)
          meta (or meta "")
          mime (some-> meta
                       (str/replace-first #"^data:" "")
                       (str/split #";" 2)
                       first
                       str/trim
                       content/nonblank)
          b64 (content/nonblank b64)]
      (when b64
        {:data b64
         :mimeType mime}))))

(defn base64-bytes
  [b64]
  (when-let [b64 (content/nonblank b64)]
    (let [len (count b64)
          padding (cond
                    (str/ends-with? b64 "==") 2
                    (str/ends-with? b64 "=") 1
                    :else 0)]
      (max 0 (- (js/Math.floor (* 3 (/ len 4))) padding)))))
(defn media-part->eta-mu-attachment [part]
  (let [part-type (content-part-type part)]
    (when (contains? #{"image" "audio" "video" "document"} part-type)
      (let [raw-data (content/nonblank (:data part))
            parsed (when (= "image" part-type)
                     (data-url->image-attachment raw-data))
            data (or (:data parsed) (xturn-media/strip-data-url raw-data))
            mime-type (content/nonblank
                       (some-> (or (:mimeType part) (:mimeType parsed))
                               name))
            filename (content/nonblank (:filename part))]
        (when data
          (cond-> {:type part-type
                   :data data}
            mime-type (assoc :mimeType mime-type)
            (and filename (not= part-type "audio")) (assoc :filename filename)))))))
(defn file-processor-style-marker
  [media-part]
  (let [t (content-part-type media-part)
        mime (or (content/nonblank (:mimeType media-part))
                 (when (= t "audio") "audio/mpeg")
                 (when (= t "image") "image/png"))
        name (case t
               "audio" (if (= mime "audio/wav") "attached-audio.wav" "attached-audio.mp3")
               "image" "attached-image"
               "video" "attached-video"
               "document" "attached-document"
               (str "attached-" t))
        bytes (or (:size media-part) (base64-bytes (:data media-part)))]
    (cond
      (= t "audio")
      (str "<file name=\"" name "\">[Audio attached: " mime ", " (or bytes "?") " bytes.]</file>\n")

      (= t "image")
      (str "<file name=\"" name "\"></file>\n")

      (= t "video")
      (str "<file name=\"" name "\">[Video attached: " mime ", " (or bytes "?") " bytes.]</file>\n")

      (= t "document")
      (str "<file name=\"" name "\">[Document attached: " mime ", " (or bytes "?") " bytes.]</file>\n")

      :else "")))

(defn send-user-message-with-timeout!
  "Send the user message to the provider session.

   When `timeout-ms` is a positive number the send is raced against a rejection
   timer. When it is nil, zero, or negative the turn runs unbounded. The event
   runner supplies its deployment-only bound here; interactive chat keeps the
   independent global turn setting, whose default is unbounded."
  [session content timeout-ms]
  (let [timeout-ms (when (number? timeout-ms) timeout-ms)]
    (if (and timeout-ms (pos? timeout-ms))
      (xpromise/with-timeout-error
       (send-user-message! session content)
       timeout-ms
       (ex-info (str "Agent turn timed out after " timeout-ms "ms")
                {:error/kind :agent-turn-timeout
                 :timeout-ms timeout-ms}))
      (send-user-message! session content))))

(defn- agent-turn-timeout?
  [err]
  (= :agent-turn-timeout (:error/kind (ex-data err))))

(defn- abort-with-grace!
  [config abort! err {:keys [run-id conversation-id session-id]}]
  ;; Keep provider invocation outside an `await` form. The CLJS async transform
  ;; otherwise awaits this first argument before constructing the timeout
  ;; wrapper, which would let a hung provider abort bypass the grace entirely.
  (xpromise/with-timeout-error
   (abort! (ex-message err))
   (or (:agent-turn-abort-grace-ms config) 5000)
   (ex-info "Provider abort did not settle before its safety grace elapsed"
            {:error/kind :agent-turn-abort-grace-timeout
             :run-id run-id
             :conversation-id conversation-id
             :session-id session-id})))

(defn- ^:async abort-timed-out-turn!
  [config abort! err {:keys [run-id conversation-id session-id] :as turn}]
  (try
    ;; Preserve the same clean error text in the durable failure record whether
    ;; the provider abort succeeds or fails. request-abort! records the reason
    ;; before delegating to the provider session.
    (await (abort-with-grace! config abort! err turn))
    (catch :default abort-error
      ;; Releasing the FIFO after a failed/hung abort would let the old provider
      ;; execute tools concurrently with the next event. Fail-stop instead:
      ;; PM2/Compose restarts Knoxx and durable admission state drives replay.
      (errors/log-error! :agent-turn/provider-abort-failed
                         {:run-id run-id
                          :conversation-id conversation-id
                          :session-id session-id
                          :timeout-error (ex-message err)}
                         abort-error)
      (xturn-node/terminate-process! 1)
      ;; process.exit does not return in production. This throw keeps test or
      ;; embedded runtimes that replace it from continuing into FIFO release.
      (throw
       (ex-info "Backend termination required after provider abort failure"
                {:error/kind :agent-turn-provider-abort-failed
                 :run-id run-id
                 :conversation-id conversation-id
                 :session-id session-id}
                abort-error)))))

(defn- build-turn-prompt
  "Build prompt content and its logging counts before starting provider work."
  [message agent-spec prompt-content-parts hydration memory-hydration]
  (let [parts (or prompt-content-parts [])
        media-parts (->> parts (keep media-part->eta-mu-attachment) vec)
        omitted-count (max 0 (- (count parts) (count media-parts)))
        turn-message (or (content/nonblank message)
                         (content/nonblank (:task-prompt agent-spec))
                         "")
        attachment-markers (when (seq media-parts)
                             (apply str (map file-processor-style-marker media-parts)))
        base-text (str (or attachment-markers "")
                       (build-agent-user-message turn-message hydration memory-hydration))
        final-text (cond-> (decision-input/append-context base-text (:decision-encounters agent-spec))
                     (pos? omitted-count)
                     (str "\n\n" "[Note: " omitted-count " unsupported attachment(s) were omitted for this model/runtime.]"))
        content (xturn-prompt/prompt-content media-parts final-text)]
    {:content content :parts-count (count parts) :media-parts-count (count media-parts)
     :omitted-count omitted-count}))

(defn- ^:async await-provider-prompt!
  "Settle provider output or refusal, retaining fail-stop behavior for an unsafe abort."
  [config state session prompt abort! unsubscribe success! failure!]
  (try
    (await (send-user-message-with-timeout! session (:content prompt) (:agent-turn-timeout-ms config)))
    (agent-ctx/clear-context!)
    (unsubscribe)
    (await (success!))
    (catch :default err
      ;; A timed-out provider must stop before failure settlement can release its FIFO.
      (when (agent-turn-timeout? err)
        (await (abort-timed-out-turn!
                config abort! err (select-keys state [:run-id :conversation-id :session-id]))))
      (agent-ctx/clear-context!)
      (unsubscribe)
      (turn-control/unregister-active-turn! (:conversation-id state) (:run-id state))
      (await (failure! err)))))

(defn- ^:async prompt-with-owned-observer!
  "Bind the invocation's observer to every terminal continuation before awaiting the provider."
  [config session-id run-id conversation-id started-ms model-id mode
   session message prompt-content-parts hydration memory-hydration
   persisted-request-messages agent-spec event-stream-sink]
  (let [state (assoc (stream/make-stream-state run-id conversation-id session-id (now-iso) started-ms xturn-node/random-uuid!)
                     :event-stream-sink event-stream-sink)
        abort! (fn [reason] (stream/request-abort! state session reason))
        _registered (stream/register-active-turn! state abort! agent-spec)
        unsubscribe (subscribe! session (stream/build-subscribe-handler state session))
        prompt (build-turn-prompt message agent-spec prompt-content-parts hydration memory-hydration)]
    (xturn-prompt/log-prompt! (assoc prompt :run-id run-id :session-id session-id
                                  :conversation-id conversation-id :model-id model-id :mode mode))
    (agent-ctx/set-context! {:session-id session-id :conversation-id conversation-id
                             :run-id run-id :agent-spec agent-spec})
    (await (await-provider-prompt!
            config state session prompt abort! unsubscribe
            #(finalize-turn-success! config state session run-id conversation-id session-id started-ms model-id mode
                                     hydration memory-hydration persisted-request-messages agent-spec)
            #(finalize-turn-failure! config state session run-id conversation-id session-id started-ms
                                     hydration memory-hydration persisted-request-messages agent-spec %)))))

(defn ^:async prompt-and-await!
  "Send, stream and finalize a turn, releasing only its explicitly owned observer.
   The legacy arity does not claim an ambient observer installed by another turn."
  ([config session-id run-id conversation-id started-ms model-id mode
    session message prompt-content-parts hydration memory-hydration
    persisted-request-messages agent-spec]
   (prompt-with-owned-observer! config session-id run-id conversation-id started-ms model-id mode
                                session message prompt-content-parts hydration memory-hydration
                                persisted-request-messages agent-spec nil))
  ([config session-id run-id conversation-id started-ms model-id mode
    session message prompt-content-parts hydration memory-hydration
    persisted-request-messages agent-spec event-stream-sink]
   (prompt-with-owned-observer! config session-id run-id conversation-id started-ms model-id mode
                                session message prompt-content-parts hydration memory-hydration
                                persisted-request-messages agent-spec event-stream-sink)))

(defn studio-stream-path
  [value]
  (xturn-media/studio-stream-path value))

(defn read-workspace-media-data-url!
  [runtime config max-bytes raw-path fallback-mime label]
  (let [normalized (media/normalize-tool-path-arg raw-path)
        {:keys [absolute relative]} (media/resolve-workspace-media-path runtime config normalized)
        mime (or (media/workspace-media-mime-type relative) fallback-mime)]
    (xturn-node/file-data-url! absolute mime label max-bytes)))


(defn fetch-media-data-url!
  [runtime config auth-context max-bytes url fallback-mime label]
  (if-let [stream-path (studio-stream-path url)]
    (read-workspace-media-data-url! runtime config max-bytes stream-path fallback-mime label)
    (xturn-media/fetch-data-url! url fallback-mime label max-bytes auth-context)))

(defn ^:async materialize-part!
  [runtime config auth-context max-bytes part]
  (let [part-type (content-part-type part)]
    (cond
      (not (#{"image" "audio"} part-type)) part

      (xturn-media/data-url? (:data part)) part

      (xturn-media/media-url? (:data part))
      (let [data-url (await (fetch-media-data-url! runtime config auth-context max-bytes (:data part)
                                                   (if (= part-type "audio") "audio/mpeg" "image/png")
                                                   part-type))]
        (-> part
            (dissoc :url)
            (assoc :data data-url)))

      (xturn-media/media-url? (:url part))
      (let [data-url (await (fetch-media-data-url! runtime config auth-context max-bytes (:url part)
                                                   (if (= part-type "audio") "audio/mpeg" "image/png")
                                                   part-type))]
        (-> part
            (dissoc :url)
            (assoc :data data-url)))

      :else part)))
(defn materialize-content-parts!
  [runtime config model-id auth-context max-bytes parts]
  (let [parts (vec (or parts []))
        should-materialize? (some (fn [part]
                                    (let [part-type (content-part-type part)]
                                      (and (#{"image" "audio"} part-type)
                                           (model-supports-input? config model-id part-type))))
                                  parts)]
    (if-not (and (seq parts) should-materialize?)
      (js/Promise.resolve parts)
      (xpromise/all-vec
       (mapv #(materialize-part! runtime config auth-context max-bytes %) parts)))))
(defn- ^:async emit-hydration-event!
  [run-id conversation-id session-id event-type hydration resource-patch]
  (let [event (run-payload/tool-event-payload run-id conversation-id session-id event-type
                                  (merge {:status (if (:graph? hydration) (name (:status hydration)) "ok")
                                          :hits (count (or (:hits hydration) (:results hydration) []))
                                          :elapsed_ms (:elapsedMs hydration)}
                                         (select-keys hydration [:failure :field-status :graph?])))]
    (update-run! run-id
                 (fn [run]
                   (-> run
                       (update :resources merge resource-patch)
                       (assoc :updated_at (now-iso)))))
    (append-run-event! run-id event)
    (await (run-events/flush! run-id))
    (broadcast-ws-session! session-id "events" event)))

(defn- ^:async publish-hydration!
  [run-id conversation-id session-id hydration memory-hydration]
  (when hydration
    (await (emit-hydration-event! run-id conversation-id session-id "passive_hydration"
                                  hydration {:passiveHydration (select-keys hydration [:query :tokens :database :elapsedMs :results])})))
  (when (or (:graph? memory-hydration) (seq (:hits memory-hydration)))
    (await (emit-hydration-event! run-id conversation-id session-id "memory_hydration"
                                  memory-hydration {:memoryHydration (hydration/memory-hydration-projection memory-hydration)}))))

(defn- resolve-turn-model
  "Resolve the effective model-id for a turn."
  [config model agent-spec]
  (let [requested-model (or model (:model agent-spec))]
    (or requested-model (:llmModel (ensure-settings! config)) (:proxx-default-model config))))

(defn- resolve-turn-thinking-level
  "Resolve the effective thinking level for a turn."
  [config model-id thinking-level agent-spec]
  (let [thinking-level-raw (or thinking-level (:thinking-level agent-spec))
        parsed-thinking-level (when thinking-level-raw
                                (normalize-thinking-level thinking-level-raw))]
    {:thinking-level-raw thinking-level-raw
     :parsed-thinking-level parsed-thinking-level
     :thinking-level (effective-thinking-level config model-id (or parsed-thinking-level
                                                                   thinking-level-raw
                                                                   (:agent-thinking-level config)
                                                                   "off"))}))

(defn- ^:async prepare-turn-context
  "Resolve turn parameters from the request and agent-spec.
   Returns a map of resolved values or throws for invalid inputs."
  [runtime config {:keys [conversation-id session-id message template-context model mode run-id auth-context thinking-level agent-spec]}]
  (let [conversation-id (or conversation-id (xturn-node/random-uuid!))
        session-id (ensure-session-id session-id)
        auth-context (auth-context-for-agent-turn auth-context agent-spec)
        {:keys [auth-context agent-spec encounter-context]}
        (await (character-context/prepare! runtime config auth-context agent-spec template-context))
        _ (ensure-conversation-access! auth-context conversation-id)
        _ (remember-conversation-access! auth-context conversation-id)
        mode (or mode "direct")
        model-id (resolve-turn-model config model agent-spec)
        {:keys [thinking-level-raw parsed-thinking-level thinking-level]} (resolve-turn-thinking-level config model-id thinking-level agent-spec)
        run-id (or run-id (xturn-node/random-uuid!))
        started-at (now-iso)
        started-ms (.now js/Date)
        existing-messages (vec (or (:messages (session-store/get-session-sync session-id)) []))
        seeded-messages (transcript/ensure-system-message existing-messages agent-spec)
        auth-extra (auth-snapshot auth-context)]
    (when (and thinking-level-raw (nil? parsed-thinking-level))
      (throw (js/Error. (str "Unsupported thinking level: " thinking-level-raw ". Expected one of off, minimal, low, medium, high, xhigh."))))
    {:conversation-id conversation-id :session-id session-id :run-id run-id
     :auth-context auth-context :auth-extra auth-extra :agent-spec agent-spec
     :mode mode :model-id model-id :thinking-level thinking-level
     :started-at started-at :started-ms started-ms
     :seeded-messages seeded-messages :message message
     :memory-query (decision-input/memory-query message encounter-context)}))

(defn ^:async hydrate-and-materialize!
  "Run passive hydration, memory hydration, content materialization, and session
   setup in parallel.  Returns a promise that resolves to the 4-element result vector."
  [runtime config {:keys [conversation-id session-id message memory-query mode model-id thinking-level agent-spec auth-context startup-owner] :as ctx} content-parts]
  (let [max-bytes 32000000]
    (await
     (xpromise/all-vec
      [(passive-hydration! runtime config mode message auth-context)
       (passive-memory-hydration! config conversation-id (or memory-query message) auth-context agent-spec
                                  (character-context/query-authority-resolver runtime config ctx))
       (materialize-content-parts! runtime config model-id auth-context max-bytes content-parts)
       (initial-admission/construct-session! ctx
        #(ensure-agent-session! runtime config conversation-id model-id auth-context thinking-level session-id agent-spec startup-owner))]))))

(defn ^:async send-agent-turn!
  "Orchestrate a full agent turn: validate, hydrate, create run, prompt, and stream."
  [runtime config {:keys [content-parts] :as turn-request}]
  (let [ctx (assoc (await (prepare-turn-context runtime config turn-request))
                   :startup-owner (xturn-node/random-uuid!) :startup-failed* (atom false))
        {:keys [conversation-id model-id message]} ctx]
    (await (policy/enforce-chat-policy! (:auth-context ctx) model-id))
    (maybe-prime-session-title! runtime config conversation-id message)
    (let [ctx (await (character-context/refresh-query! runtime config ctx))
          results (await (initial-admission/hydrate! ctx #(hydrate-and-materialize! runtime config ctx content-parts)))]
      (await (startup/start! runtime config ctx results
                             {:install-event-sink! install-openplanner-event-sink!
                              :publish-hydration! publish-hydration!
                              :finalize-failure! finalize-turn-failure!
                              :prompt! prompt-and-await!})))))
