(ns knoxx.backend.infra.agent.session
  (:require [clojure.string :as str]
            [clojure.set :as set]
            [knoxx.backend.domain.models :refer [normalize-thinking-level effective-thinking-level resolve-model-contract]]
            [knoxx.backend.extern.eta-mu :as eta-mu-extern]
            [knoxx.backend.extern.extension :as extension-extern]
            [knoxx.backend.extern.tools :as tools-extern]
            [knoxx.backend.infra.character.authority :as character-authority]
            [knoxx.backend.infra.character.mode-runtime :as mode-runtime]
            [knoxx.backend.infra.actor.acting :as acting]
            [knoxx.backend.infra.agent.content-codec :as content-codec]
            [knoxx.backend.infra.agent.history :as history]
            [knoxx.backend.infra.agent.provider.eta-mu :as eta-mu-provider]
            [knoxx.backend.infra.agent.session-registry :as session-registry]
            [knoxx.backend.infra.agent.tool-catalog :as tool-catalog]
            [knoxx.backend.infra.http :refer [no-content?]]
            [knoxx.backend.infra.stores.composite-message-source :refer [->CompositeMessageSource]]
            [knoxx.backend.infra.stores.openplanner-message-source :refer [->OpenPlannerMessageSource]]
            [knoxx.backend.infra.stores.mongo-message-source :refer [->MongoMessageSource]]
            [knoxx.backend.domain.extension-runtime :as ext-runtime]
            [knoxx.backend.domain.actor.mailbox :as actor-mailbox]
            [knoxx.backend.domain.agent.agent-context :as agent-context]
            [knoxx.backend.shape.agent :refer [set-thinking-level!]]))

(defonce sessions* (atom {}))

(def ^:private inactive-ttl-ms session-registry/default-inactive-ttl-ms)
(def ^:private sweep-interval-ms 300000)
(def ^:private active-session-registry
  (session-registry/atom-registry sessions* {:inactive-ttl-ms inactive-ttl-ms}))

;; ─── Private helpers ────────────────────────────────────────────────────────


(defn- restore-agent-context!
  [previous]
  (if previous
    (agent-context/set-context! previous)
    (agent-context/clear-context!)))

(defn- wrap-tool-execute-with-agent-context!
  [tool context]
  (when-let [execute (eta-mu-extern/tool-execute tool)]
    (eta-mu-extern/set-tool-execute!
     tool
     (fn [& args]
       (let [previous (agent-context/get-context)]
         (agent-context/set-context! context)
         (try
           (eta-mu-extern/with-promise-finally
            (apply execute args)
            (fn [] (restore-agent-context! previous)))
           (catch :default err
             (restore-agent-context! previous)
             (throw err)))))))
  tool)

(defn- wrap-custom-tools-with-agent-context!
  [custom-tools context]
  (when custom-tools
    (doseq [tool (eta-mu-extern/tool-seq custom-tools)]
      (wrap-tool-execute-with-agent-context! tool context)))
  custom-tools)

(defn prune-session-messages
  [agent-spec messages]
  (history/prune-session-messages agent-spec messages))

(defn- ^:async register-actor-live-route!
  [runtime conversation-id session-id agent-spec]
  (when-let [actor-id (some-> (:actor-id agent-spec) str str/trim not-empty)]
    (try
      (await (actor-mailbox/register-live-session!
              runtime
              {:actor-id actor-id
               :conversation-id conversation-id
               :session-id session-id
               :contract-id (some-> (:contract-id agent-spec) str str/trim not-empty)
               :source {:registeredBy "agent-runtime"
                        :contractId (:contract-id agent-spec)}}))
      (catch :default err
        (.warn js/console "[actor-mailbox] failed to register live actor route" (.-message err))))))

;; ─── Session registry ────────────────────────────────────────────────────────

(defn active-agent-session
  [conversation-id]
  (session-registry/get-active-session active-session-registry conversation-id))

(defn- active-session-entry
  [conversation-id]
  (session-registry/get-active-session-entry active-session-registry conversation-id))

(defn- start-sweep!
  []
  (js/setInterval
   (fn []
     (session-registry/sweep-expired-sessions! active-session-registry (js/Date.now)))
   sweep-interval-ms))

(start-sweep!)

;; ─── Media helpers ───────────────────────────────────────────────────────────

(defn fetch-b64!
  [url media-type]
  (content-codec/fetch-b64! url media-type))

(defn materialize!
  [part]
  (content-codec/materialize! part))

;; ─── Session hydration ──────────────────────────────────────────────────────

(defn ^:async rehydrate-session-manager!
  [message-source session-manager conversation-id agent-spec]
  (await (history/rehydrate-session-manager! message-source session-manager conversation-id agent-spec)))

;; ─── Runtime setup ───────────────────────────────────────────────────────────

(defn ^:async ensure-eta-mu-runtime!
  [runtime config]
  (await (eta-mu-provider/ensure-runtime! (eta-mu-provider/eta-mu-provider runtime config))))

;; ─── Session creation ────────────────────────────────────────────────────────

(defn- visible-session-signature
  [runtime config auth-context agent-spec]
  (tool-catalog/visible-session-signature runtime config auth-context agent-spec))

(defn- registered-focused-tool
  [current-context tool]
  (let [registered (tools-extern/registered-tool tool)
        execute (:execute registered)
        scope {:actor-id (:actorId current-context)
               :org-id (get-in current-context [:org :id])
               :membership-id (get-in current-context [:membership :id])}]
    (assoc registered :execute
           (fn [id args signal update!]
             (acting/run-as! scope #(execute id args signal update!))))))

(defn- ^:async refresh-focused-catalog!
  [runtime config auth-context agent-spec allowed-tool-ids session-id conversation-id]
  (let [current-context (await (character-authority/resolve-current! config auth-context agent-spec))
        current-allowed (set/intersection allowed-tool-ids
                                          (tool-catalog/focused-authorized-tool-ids config current-context agent-spec))
        current-tool-context (tool-catalog/effective-tool-auth-context current-context current-allowed)
        builtin (eta-mu-extern/builtin-tool-closures
                 (:workspace-root config) (tool-catalog/builtin-tools runtime config current-tool-context agent-spec))
        closures (wrap-custom-tools-with-agent-context!
                  (tool-catalog/custom-tools runtime config current-tool-context agent-spec current-allowed)
                  {:session-id session-id :conversation-id conversation-id :agent-spec agent-spec})]
    {:allowed-ids current-allowed
     ;; graph_query lacks per-memory visibility; do not newly expose it here.
     :catalog (->> (concat (eta-mu-extern/tool-seq builtin) (eta-mu-extern/tool-seq closures))
                   (map #(registered-focused-tool current-context %))
                   (remove #(= "graph_query" (:id %))) vec)}))

(defn- ^:async session-provider-tools
  [runtime config tool-auth-context agent-spec allowed-tool-ids _model-id session-id conversation-id]
  (if-let [mode-configuration (:tool-modes agent-spec)]
    (let [refresh! (partial refresh-focused-catalog! runtime config tool-auth-context agent-spec allowed-tool-ids session-id conversation-id)
          controller (await (mode-runtime/make-controller! mode-configuration
                                                          {:character (:character-context agent-spec)
                                                           :conversation-id conversation-id :session-id session-id} refresh!))]
      {:custom-tools (tools-extern/focused-tools controller)
       :tool-name-allowlist ["capabilities" "invoke"] :mode-controller controller})
    (let [builtin-tools (tool-catalog/builtin-tools runtime config tool-auth-context agent-spec)
          custom-tools (wrap-custom-tools-with-agent-context!
                        (tool-catalog/custom-tools runtime config tool-auth-context agent-spec allowed-tool-ids)
                        {:session-id session-id :conversation-id conversation-id :agent-spec agent-spec})]
      {:custom-tools custom-tools
       :tool-name-allowlist (tool-catalog/tool-runtime-names builtin-tools custom-tools)})))

(defn ^:async create-session-manager!
  ([runtime config conversation-id model-id] (create-session-manager! runtime config conversation-id model-id nil (:agent-thinking-level config)))
  ([runtime config conversation-id model-id auth-context] (create-session-manager! runtime config conversation-id model-id auth-context (:agent-thinking-level config)))
  ([runtime config conversation-id model-id auth-context thinking-level]
   (create-session-manager! runtime config conversation-id model-id auth-context thinking-level nil))
  ([runtime config conversation-id model-id auth-context thinking-level session-id]
   (create-session-manager! runtime config conversation-id model-id auth-context thinking-level session-id nil))
  ([runtime config conversation-id model-id auth-context thinking-level session-id agent-spec]
   (let [{:keys [auth-storage model-registry settings-manager loader runtime-dir]} (await (ensure-eta-mu-runtime! runtime config))
         thinking-level (effective-thinking-level config model-id (or (normalize-thinking-level thinking-level)
                                                                      thinking-level
                                                                      (:agent-thinking-level config)
                                                                      "off"))
         model-provider-id (or (some-> (resolve-model-contract config model-id) :provider)
                               "proxx")
         provider (eta-mu-provider/eta-mu-provider runtime config)
          model (eta-mu-provider/resolve-model provider
                                               model-registry
                                               model-provider-id
                                               model-id
                                               (:proxx-default-model config))
          allowed-tool-ids (if (:tool-modes agent-spec)
                             (tool-catalog/focused-authorized-tool-ids config auth-context agent-spec)
                             (tool-catalog/allowed-tool-ids config auth-context agent-spec))
          tool-auth-context (tool-catalog/effective-tool-auth-context auth-context allowed-tool-ids)
          {:keys [custom-tools tool-name-allowlist mode-controller]} (await (session-provider-tools runtime config tool-auth-context agent-spec
                                                                                                  allowed-tool-ids model-id session-id conversation-id))
          preferred-session-id (some-> session-id str str/trim not-empty)
          message-source (->CompositeMessageSource
                           (->OpenPlannerMessageSource config)
                          (->MongoMessageSource preferred-session-id))]
     (if (no-content? model)
       (js/Promise.reject (js/Error. (str "No eta-mu model configured for " model-id)))
       (let [session-manager (eta-mu-extern/make-session-manager! (:workspace-root config) preferred-session-id)]
         (eta-mu-extern/append-model-change! session-manager model-provider-id model-id)
         (eta-mu-extern/append-thinking-level-change! session-manager thinking-level)
         (let [{:keys [session-manager]} (await (rehydrate-session-manager! message-source session-manager conversation-id agent-spec))
               session (await (eta-mu-provider/create-session!
                               provider
                               {:workspace-root (:workspace-root config)
                                :runtime-dir runtime-dir
                                :auth-storage auth-storage
                                :model-registry model-registry
                                :loader loader
                                :settings-manager settings-manager
                                :session-manager session-manager
                                :model model
                                :thinking-level thinking-level
                                :tool-name-allowlist tool-name-allowlist
                                :custom-tools custom-tools
                                :tools-choice (:tools-choice agent-spec)
                                :system-prompt (when mode-controller (:system-prompt agent-spec))
                                :materialize! materialize!}))]
           (set-thinking-level! session thinking-level)
           (cond-> session mode-controller (assoc :mode-controller mode-controller))))))))

(defn- claim-startup-session!
  "Join a pending construction; established sessions never become startup-owned."
  [conversation-id session owner]
  (swap! sessions*
         (fn [entries]
           (let [entry (get entries conversation-id)]
             (if (and (identical? session (:session entry)) (::startup-owners entry))
               (assoc entries conversation-id
                      (if owner (update entry ::startup-owners conj owner)
                          (dissoc entry ::startup-owners)))
               entries)))))

(defn- shutdown-session-entry! [conversation-id entry]
  (let [ctx (ext-runtime/build-extension-ctx
             (extension-extern/empty-event-payload) {}
             :conversation-id conversation-id :session-id (:session-id entry))]
    (ext-runtime/dispatch-event "session_shutdown"
                               (extension-extern/event-payload {:conversationId conversation-id}) ctx)))

(defn settle-startup-session!
  "Promote a successful construction or release only this pending startup claim.
   The last failed claimant removes an unadmitted construction. A replacement,
   established session or another pending claimant is never removed."
  [conversation-id owner admitted?]
  (loop []
    (let [before @sessions* entry (get before conversation-id) owners (::startup-owners entry)]
      (when (and owner (contains? owners owner))
        (let [remaining (disj owners owner)
              next-entry (cond admitted? (dissoc entry ::startup-owners)
                               (seq remaining) (assoc entry ::startup-owners remaining))
              after (if next-entry (assoc before conversation-id next-entry) (dissoc before conversation-id))]
          (if (compare-and-set! sessions* before after)
            (when-not next-entry (shutdown-session-entry! conversation-id entry))
            (recur))))))
  nil)

(defn ^:async construct-session-and-ext-ctx!
  ([runtime config conversation-id model-id auth-context thinking-level session-id agent-spec current-tool-signature life-cycle-event-name]
   (construct-session-and-ext-ctx! runtime config conversation-id model-id auth-context thinking-level
                                   session-id agent-spec current-tool-signature life-cycle-event-name nil))
  ([runtime config conversation-id model-id auth-context thinking-level session-id agent-spec current-tool-signature life-cycle-event-name owner]
  (let [next-session (await (create-session-manager! runtime config conversation-id model-id auth-context thinking-level session-id agent-spec))
        ctx (ext-runtime/build-extension-ctx runtime config
                                             :conversation-id conversation-id
                                             :session-id session-id
                                             :model-id model-id
                                             :auth-context auth-context)]
    (ext-runtime/dispatch-event life-cycle-event-name
                                (extension-extern/event-payload {:conversationId conversation-id
                                                                 :sessionId session-id})
                                ctx)
    (session-registry/put-active-session! active-session-registry
                                          conversation-id
                                          (cond-> {:session next-session
                                           :model-id model-id
                                           :tool-signature current-tool-signature
                                           :session-id session-id
                                           :actor-id (:actor-id agent-spec)}
                                            owner (assoc ::startup-owners #{owner})))
    (register-actor-live-route! runtime conversation-id session-id agent-spec)
    next-session)))

(defn- reuse-session! [runtime conversation-id session-id agent-spec session thinking-level owner]
  (claim-startup-session! conversation-id session owner)
  (set-thinking-level! session thinking-level)
  (register-actor-live-route! runtime conversation-id session-id agent-spec)
  (js/Promise.resolve session))

(defn ensure-agent-session!
  ([runtime config conversation-id model-id] (ensure-agent-session! runtime config conversation-id model-id nil (:agent-thinking-level config)))
  ([runtime config conversation-id model-id auth-context] (ensure-agent-session! runtime config conversation-id model-id auth-context (:agent-thinking-level config)))
  ([runtime config conversation-id model-id auth-context thinking-level]
   (ensure-agent-session! runtime config conversation-id model-id auth-context thinking-level nil))
  ([runtime config conversation-id model-id auth-context thinking-level session-id]
   (ensure-agent-session! runtime config conversation-id model-id auth-context thinking-level session-id nil))
  ([runtime config conversation-id model-id auth-context thinking-level session-id agent-spec]
   (ensure-agent-session! runtime config conversation-id model-id auth-context thinking-level session-id agent-spec nil))
  ([runtime config conversation-id model-id auth-context thinking-level session-id agent-spec owner]
   (let [thinking-level (effective-thinking-level config model-id (or (normalize-thinking-level thinking-level)
                                                                      thinking-level
                                                                      (:agent-thinking-level config)
                                                                      "off"))
         current-tool-signature (visible-session-signature runtime config auth-context agent-spec)
         construct-this-session! (partial construct-session-and-ext-ctx! runtime config conversation-id model-id
                                          auth-context thinking-level session-id agent-spec current-tool-signature)]
     (if-let [entry (active-session-entry conversation-id)]
       (let [session (:session entry)
             active-model (:model-id entry)
             active-tool-signature (:tool-signature entry)]
         (if (and (some? session)
                  (= (str active-model) (str model-id))
                  (= (str (or active-tool-signature "")) (str (or current-tool-signature ""))))
           (reuse-session! runtime conversation-id session-id agent-spec session thinking-level owner)
           (construct-this-session! "session_switch" owner)))
       (construct-this-session! "session_start" owner)))))

(defn remove-agent-session!
  "Dispatch session_shutdown to extensions, then release the in-process session entry."
  [conversation-id]
  (when-let [entry (active-session-entry conversation-id)]
    (shutdown-session-entry! conversation-id entry))
  (session-registry/remove-active-session! active-session-registry conversation-id)
  nil)
